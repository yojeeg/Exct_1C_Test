
&НаКлиенте
Процедура ВыполнитьПроверку(Команда)
	
	ВыполнитьПроверкуСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ТолькоДляНеЗакрытыхДоговоровГПХ(Команда)
	
	ВыполнитьПроверкуСервер(Истина);
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьПроверкуСервер(ТолькоДГПХ = Ложь)
	
	Объект.ФизическиеЛица.Очистить();

	Запрос = Новый Запрос;
	
	Если Не ТолькоДГПХ Тогда
	
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ФизическиеЛица.Ссылка КАК ФизическоеЛицо,
		|	ФизическиеЛица.ИНН
		|ИЗ
		|	Справочник.ФизическиеЛица КАК ФизическиеЛица";
		
	Иначе
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДоговорРаботыУслуги.ФизическоеЛицо
		|ПОМЕСТИТЬ ВТ_ДГПХ
		|ИЗ
		|	Документ.ДоговорРаботыУслуги КАК ДоговорРаботыУслуги
		|ГДЕ
		|	ДоговорРаботыУслуги.ДатаОкончания = ДАТАВРЕМЯ(3099, 12, 31)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ФизическиеЛица.Ссылка КАК ФизическоеЛицо,
		|	ФизическиеЛица.ИНН
		|ИЗ
		|	Справочник.ФизическиеЛица КАК ФизическиеЛица
		|ГДЕ
		|	ФизическиеЛица.Ссылка В
		|			(ВЫБРАТЬ
		|				ВТ_ДГПХ.ФизическоеЛицо
		|			ИЗ
		|				ВТ_ДГПХ КАК ВТ_ДГПХ)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ДГПХ";
	
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		Данные = Результат.Выгрузить();
		Данные.Колонки.Добавить("Причина", Новый ОписаниеТипов("Строка"));
		Данные.Колонки.Добавить("ОшибокНет", Новый ОписаниеТипов("Булево"));
		
		Для Каждого Строка Из Данные Цикл
			
			Если НЕ РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(СокрЛП(Строка.ИНН), Ложь, Строка.Причина) Тогда
				
				НоваяСтрока = Объект.ФизическиеЛица.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьПроверкуСервер()
