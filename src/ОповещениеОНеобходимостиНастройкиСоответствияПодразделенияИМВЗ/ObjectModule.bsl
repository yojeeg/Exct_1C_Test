Функция СведенияОВнешнейОбработке() Экспорт
	
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке();
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиДополнительнаяОбработка();
	ПараметрыРегистрации.Наименование = ЭтотОбъект.Метаданные().Синоним;
	ПараметрыРегистрации.Версия = "1.02";
	ПараметрыРегистрации.Информация = ЭтотОбъект.Метаданные().Синоним;
	ПараметрыРегистрации.БезопасныйРежим = Ложь;
	
	Команда = ПараметрыРегистрации.Команды.Добавить();
	Команда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовСерверногоМетода();
	Команда.Представление = ЭтотОбъект.Метаданные().Синоним;
	Команда.Идентификатор = ЭтотОбъект.Метаданные().Имя;
	
	Возврат ПараметрыРегистрации;	
	
КонецФункции

Процедура ВыполнитьКоманду(ИдентификаторКоманды = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПодразделенияОрганизаций.Ссылка КАК Ссылка,
	|	ППФ_СоответствиеПодразделенийИМВЗСрезПоследних.МВЗ КАК МВЗ
	|ИЗ
	|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ППФ_СоответствиеПодразделенийИМВЗ.СрезПоследних КАК ППФ_СоответствиеПодразделенийИМВЗСрезПоследних
	|		ПО ПодразделенияОрганизаций.Ссылка = ППФ_СоответствиеПодразделенийИМВЗСрезПоследних.Подразделение
	|ГДЕ
	|	ПодразделенияОрганизаций.Наименование ПОДОБНО ""%гентство%""
	|	И НЕ ПодразделенияОрганизаций.Расформировано
	|	И ПодразделенияОрганизаций.Сформировано
	|	И ППФ_СоответствиеПодразделенийИМВЗСрезПоследних.МВЗ ЕСТЬ NULL";
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда 
		// Получатель письма
		ДолжностьНачальникаОтделаУчетаЗарабонтнойПлаты = Справочники.ШтатноеРасписание.НайтиПоНаименованию("Начальник отдела /Отдел учета заработной платы и агентского вознаграждения/");
		ПолучателиКопииПисьма = Новый Массив;
		ПолучателиКопииПисьма.Добавить("rartamonov@ppfinsurance.ru");
		ПолучателиКопииПисьма.Добавить("mponomarev@ppfinsurance.ru");
		
		ПолучателиПисьма = Новый Массив;
		
		ЗапросПочта = Новый Запрос;
		ЗапросПочта.Текст = "ВЫБРАТЬ
		|	КадроваяИсторияСотрудниковСрезПоследних.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ФизическиеЛицаКонтактнаяИнформация.АдресЭП КАК АдресЭП
		|ИЗ
		|	РегистрСведений.КадроваяИсторияСотрудников.СрезПоследних КАК КадроваяИсторияСотрудниковСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица.КонтактнаяИнформация КАК ФизическиеЛицаКонтактнаяИнформация
		|		ПО КадроваяИсторияСотрудниковСрезПоследних.ФизическоеЛицо = ФизическиеЛицаКонтактнаяИнформация.Ссылка
		|ГДЕ
		|	КадроваяИсторияСотрудниковСрезПоследних.ДолжностьПоШтатномуРасписанию = &Должность
		|	И ФизическиеЛицаКонтактнаяИнформация.Вид = &EmailРабочий";
		ЗапросПочта.УстановитьПараметр("Должность",ДолжностьНачальникаОтделаУчетаЗарабонтнойПлаты);
		ЗапросПочта.УстановитьПараметр("EmailРабочий",Справочники.ВидыКонтактнойИнформации.НайтиПоНаименованию("Email (рабочий)"));
		РезультатПочта = ЗапросПочта.Выполнить();
		Если Не РезультатПочта.Пустой() Тогда 
			ВыборкаПочта = РезультатПочта.Выбрать();
			Пока ВыборкаПочта.Следующий() Цикл 
				ПолучателиПисьма.Добавить(ВыборкаПочта.АдресЭП);
			КонецЦикла;    			
		КонецЕсли; 		
		
		МассивПодразделенийБезМВЗ = Новый Массив;
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл 
			МассивПодразделенийБезМВЗ.Добавить(Выборка.Ссылка);
		КонецЦикла;
		
		ТемаПисьма = "Необходимо настроить соответствие подразделения и МВЗ";
		ТелоПисьма = "Добрый день!"+Символы.ВК+Символы.ПС+"В системе ЗУП не задано соответствие МВЗ для следующих подразделений: ";
		Для Каждого МВЗ Из  МассивПодразделенийБезМВЗ Цикл 
			ТелоПисьма = ТелоПисьма + МВЗ + ", ";
		КонецЦикла;
		
		ТелоПисьма = Сред(ТелоПисьма,1, СтрДлина(ТелоПисьма)-2); 
		
		ППФ_Сервер.ОтправитьПисьмо(ПолучателиПисьма, ТемаПисьма, ТелоПисьма, ПолучателиКопииПисьма);
	КонецЕсли;
	
КонецПроцедуры