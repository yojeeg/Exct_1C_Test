
&НаКлиенте
Процедура ВыполнитьКоманду(ИдентификаторКоманды, ОбъектыНазначенияМассив) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ПослеОтветаНаВопрос",
	ЭтотОбъект);     	
	
	ПоказатьВопрос(Оповещение, НСтр("ru = 'Операция необратима. Рекомендуется сохранить документ перед выполнением. Продолжить?';"),
	РежимДиалогаВопрос.ДаНет,
	0,	
	КодВозвратаДиалога.Нет,
	"Очистка лишних перерасчетов");
	
КонецПроцедуры

&НаКлиенте

Процедура ПослеОтветаНаВопрос(Результат, Параметры) Экспорт 

	Если Результат = КодВозвратаДиалога.Да Тогда 		
		
		ПроверочнаяСумма = 0; // Здесь храним значение итога перерасчетов, которое не должно измениться после выполнения обработки
		
		ВременнаяТаблица = Новый Массив();
		
		ТабличаяЧасть 	= ВладелецФормы.Объект.НачисленияПерерасчет;
		ВсегоСтрокВКоллекции = ТабличаяЧасть.Количество();
		Если ВсегоСтрокВКоллекции = 0 Тогда 
			Возврат;
		КонецЕсли;
		
		Для Каждого СтрокаДанных Из ТабличаяЧасть Цикл 
			
			СтрокаТаблицы = Новый Массив();
			
			СтрокаТаблицы.Добавить(СтрокаДанных.Сотрудник);
			СтрокаТаблицы.Добавить(СтрокаДанных.ДатаНачала);
			СтрокаТаблицы.Добавить(СтрокаДанных.ДатаОкончания);
			СтрокаТаблицы.Добавить(СтрокаДанных.Результат);
			СтрокаТаблицы.Добавить(СтрокаДанных.НомерСтроки);
			
			ПроверочнаяСумма = ПроверочнаяСумма + СтрокаДанных.Результат;
			
			ВременнаяТаблица.Добавить(СтрокаТаблицы);
			
		КонецЦикла;
		
		МассивСтрокДляУдаления = ПолучитьСтрокиДляУдаления(ВременнаяТаблица, ПроверочнаяСумма);
		
		Если МассивСтрокДляУдаления = Неопределено Тогда  // Проверка, что Проверочная сумма не поменялась после чистки
			Сообщить("Не удалось произвести автоматическую чистку из-за несовпадения проверочной суммы до и после выполнения.");
			Возврат;
		КонецЕсли;
		
		Счетчик = ВсегоСтрокВКоллекции - 1;
		Пока Счетчик>=0 Цикл 
			СтрокаКоллекции = ТабличаяЧасть[Счетчик];
			Если МассивСтрокДляУдаления.Найти(СтрокаКоллекции.НомерСтроки)<>Неопределено Тогда 
				ТабличаяЧасть.Удалить(СтрокаКоллекции);
				ВладелецФормы.Модифицированность = Истина;
			КонецЕсли;
			Счетчик = Счетчик - 1;
		КонецЦикла; 
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ПолучитьСтрокиДляУдаления(МассивДанных, ПроверочнаяСумма)
	
	МассивСтрокДляУдаления = Новый Массив;
	
	ИсходнаяТаблица = Новый ТаблицаЗначений;	
	ИсходнаяТаблица.Колонки.Добавить("Сотрудник");
	ИсходнаяТаблица.Колонки.Добавить("ДатаНачала");
	ИсходнаяТаблица.Колонки.Добавить("ДатаОкончания");
	ИсходнаяТаблица.Колонки.Добавить("Результат");
	ИсходнаяТаблица.Колонки.Добавить("НомерСтроки");
	
	Для Каждого ЭлементМассива Из МассивДанных Цикл 
		
		НоваяСтрока = ИсходнаяТаблица.Добавить();
		Счетчик = 0;
		Для Каждого ЧастьДанных ИЗ ЭлементМассива Цикл 
			НоваяСтрока[Счетчик] = ЧастьДанных;
			Счетчик = Счетчик + 1;
		КонецЦикла;
		
	КонецЦикла;
	
	КопияИсходнойТаблицы = ИсходнаяТаблица.Скопировать();
	КопияИсходнойТаблицы.Колонки.Удалить("НомерСтроки");
	КопияИсходнойТаблицы.Свернуть("Сотрудник, ДатаНачала, ДатаОкончания","Результат");

	// Удалим строки, свернувшиеся в 0
	ВсегоСтрок = КопияИсходнойТаблицы.Количество();
	НомерСтроки = ВсегоСтрок - 1;
	Пока НомерСтроки >= 0  Цикл 
		СтрокаТаблицы = КопияИсходнойТаблицы[НомерСтроки];
		Если СтрокаТаблицы.Результат = 0 Тогда 
			КопияИсходнойТаблицы.Удалить(СтрокаТаблицы);
		КонецЕсли;
		НомерСтроки = НомерСтроки - 1;
	КонецЦикла;
	
	Если КопияИсходнойТаблицы.Количество()>0 Тогда 
		Для Каждого СтрокаИсхТаблицы Из ИсходнаяТаблица Цикл 
			НайденныеСтроки = КопияИсходнойТаблицы.НайтиСтроки(Новый Структура("Сотрудник, ДатаНачала, ДатаОкончания",СтрокаИсхТаблицы.Сотрудник,СтрокаИсхТаблицы.ДатаНачала,СтрокаИсхТаблицы.ДатаОкончания));
			Если НайденныеСтроки.Количество()=0 Тогда 
				МассивСтрокДляУдаления.Добавить(СтрокаИсхТаблицы.НомерСтроки);
			КонецЕсли;
		КонецЦикла;
	Иначе 
		МассивСтрокДляУдаления = ИсходнаяТаблица.ВыгрузитьКолонку("НомерСтроки");
	КонецЕсли;
	
	Если ПроверочнаяСумма<>КопияИсходнойТаблицы.Итог("Результат") Тогда 
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат МассивСтрокДляУдаления;
КонецФункции