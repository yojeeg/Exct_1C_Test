#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	СхемаКомпоновкиДанных = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	Настройки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);
	
	ИтоговаяТаблица = ПолучитьИтоговуюТаблицу();
	
	ВнешнийНаборДанных = Новый Структура;
	ВнешнийНаборДанных.Вставить("ИтоговаяТаблица", ИтоговаяТаблица);
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешнийНаборДанных, ДанныеРасшифровки);
	
	ДокументРезультат.Очистить();
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	ДокументРезультат.ФиксацияСлева = 3;

	
КонецПроцедуры

#КонецОбласти

#Область СведенияОВнешнейОбработке

Функция СведенияОВнешнейОбработке() Экспорт
	ПараметрыРегистрации = Новый Структура;
	ПараметрыРегистрации.Вставить("Вид", "ДополнительныйОтчет");
	ПараметрыРегистрации.Вставить("Наименование", "Отчет по взносам в ФОМС (за период)");
	ПараметрыРегистрации.Вставить("Версия", "1.0");
	ПараметрыРегистрации.Вставить("Информация", "Отчет по взносам в ФОМС (за период)");
	ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);
	
	Команды = ТаблицаКоманд();
	ДобавитьКоманду(Команды, "Отчет по взносам в ФОМС (за период)", "ОтчетПоВзносамФОМСПериод", "ОткрытиеФормы", Ложь, "");
	
	ПараметрыРегистрации.Вставить("Команды", Команды);
	
	Возврат ПараметрыРегистрации;
КонецФункции

Функция ТаблицаКоманд()
	Команды = Новый ТаблицаЗначений;
	Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));
	Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
	Возврат Команды;
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
	НоваяКоманда = ТаблицаКоманд.Добавить();
	НоваяКоманда.Представление = Представление;
	НоваяКоманда.Идентификатор = Идентификатор;
	НоваяКоманда.Использование = Использование;
	НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
	НоваяКоманда.Модификатор = Модификатор;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТекстЗапроса()
	Возврат "ВЫБРАТЬ РАЗЛИЧНЫЕ
	        |	Начисления.Ссылка КАК Ссылка
	        |ПОМЕСТИТЬ ВТ_ВИДЫ_НАЧИСЛЕНИЙ
	        |ИЗ
	        |	ПланВидовРасчета.Начисления КАК Начисления
	        |ГДЕ
	        |	Начисления.Код = ""МТПМР""
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	Ссылка
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо.Наименование КАК ФИО,
	        |	ВЫРАЗИТЬ(СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо.СтраховойНомерПФР КАК СТРОКА(20)) КАК СНИЛС,
	        |	СУММА(СведенияОДоходахСтраховыеВзносы.Сумма) КАК Доход,
	        |	СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо.ИНН КАК ИНН,
	        |	СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо КАК ФизическоеЛицо,
	        |	НАЧАЛОПЕРИОДА(СведенияОДоходахСтраховыеВзносы.Период, МЕСЯЦ) КАК Период,
	        |	СтатусыЗастрахованныхФизическихЛицСрезПоследних.ВидЗастрахованногоЛица
	        |ПОМЕСТИТЬ ВТ_ДОХОДЫ
	        |ИЗ
	        |	РегистрНакопления.СведенияОДоходахСтраховыеВзносы КАК СведенияОДоходахСтраховыеВзносы
	        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыЗастрахованныхФизическихЛиц.СрезПоследних(&КонецПериода, ) КАК СтатусыЗастрахованныхФизическихЛицСрезПоследних
	        |		ПО СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо = СтатусыЗастрахованныхФизическихЛицСрезПоследних.ФизическоеЛицо
	        |ГДЕ
	        |	СведенияОДоходахСтраховыеВзносы.ВидДохода.ВходитВБазуФОМС = ИСТИНА
	        |	И СведенияОДоходахСтраховыеВзносы.Период МЕЖДУ &НачалоПериода И &КонецПериода
	        |	И СведенияОДоходахСтраховыеВзносы.Сумма <> 0
	        |	И (СтатусыЗастрахованныхФизическихЛицСрезПоследних.ВидЗастрахованногоЛица <> ЗНАЧЕНИЕ(Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПребывающиеИностранцыНестрахуемые)
	        |			ИЛИ СтатусыЗастрахованныхФизическихЛицСрезПоследних.ВидЗастрахованногоЛица ЕСТЬ NULL )
	        |	И НЕ(СведенияОДоходахСтраховыеВзносы.Начисление В
	        |					(ВЫБРАТЬ
	        |						ВТ_ВИДЫ_НАЧИСЛЕНИЙ.Ссылка
	        |					ИЗ
	        |						ВТ_ВИДЫ_НАЧИСЛЕНИЙ КАК ВТ_ВИДЫ_НАЧИСЛЕНИЙ)
	        |				И СведенияОДоходахСтраховыеВзносы.Сумма < 51000)
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо.Наименование,
	        |	СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо.ИНН,
	        |	ВЫРАЗИТЬ(СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо.СтраховойНомерПФР КАК СТРОКА(20)),
	        |	СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо,
	        |	НАЧАЛОПЕРИОДА(СведенияОДоходахСтраховыеВзносы.Период, МЕСЯЦ),
	        |	СтатусыЗастрахованныхФизическихЛицСрезПоследних.ВидЗастрахованногоЛица
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	ФизическоеЛицо
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	Данные.ФИО,
	        |	Данные.СНИЛС,
	        |	Данные.ДоходСНачалаГода,
	        |	Данные.ДоходВТекущемМесяце,
	        |	Данные.ИНН,
	        |	Данные.Период,
	        |	Данные.ВидЗастрахованногоЛица
	        |ПОМЕСТИТЬ ВТ_ДОХОДЫ_2
	        |ИЗ
	        |	(ВЫБРАТЬ
	        |		ВТ_ДОХОДЫ.ФИО КАК ФИО,
	        |		ВТ_ДОХОДЫ.СНИЛС КАК СНИЛС,
	        |		ВТ_ДОХОДЫ.Доход КАК ДоходСНачалаГода,
	        |		0 КАК ДоходВТекущемМесяце,
	        |		ВТ_ДОХОДЫ.ИНН КАК ИНН,
	        |		ВТ_ДОХОДЫ.Период КАК Период,
	        |		ВТ_ДОХОДЫ.ВидЗастрахованногоЛица КАК ВидЗастрахованногоЛица
	        |	ИЗ
	        |		ВТ_ДОХОДЫ КАК ВТ_ДОХОДЫ
	        |	
	        |	ОБЪЕДИНИТЬ ВСЕ
	        |	
	        |	ВЫБРАТЬ
	        |		ВТ_ДОХОДЫ.ФИО,
	        |		ВТ_ДОХОДЫ.СНИЛС,
	        |		0,
	        |		ВТ_ДОХОДЫ.Доход,
	        |		ВТ_ДОХОДЫ.ИНН,
	        |		ВТ_ДОХОДЫ.Период,
	        |		ВТ_ДОХОДЫ.ВидЗастрахованногоЛица
	        |	ИЗ
	        |		ВТ_ДОХОДЫ КАК ВТ_ДОХОДЫ
	        |	ГДЕ
	        |		ВТ_ДОХОДЫ.Период МЕЖДУ НАЧАЛОПЕРИОДА(&КонецПериода, МЕСЯЦ) И &КонецПериода) КАК Данные
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ИсчисленныеСтраховыеВзносы.ФизическоеЛицо.ФИО,
	        |	ИсчисленныеСтраховыеВзносы.ФизическоеЛицо.ИНН,
	        |	ИсчисленныеСтраховыеВзносы.ФизическоеЛицо.СтраховойНомерПФР,
	        |	СУММА(ИсчисленныеСтраховыеВзносы.ФФОМС) КАК ФФОМС,
	        |	НАЧАЛОПЕРИОДА(ИсчисленныеСтраховыеВзносы.Период, МЕСЯЦ) КАК Период,
	        |	СтатусыЗастрахованныхФизическихЛицСрезПоследних.ВидЗастрахованногоЛица
	        |ПОМЕСТИТЬ ВТ_ФОМС_С_НАЧАЛА_ГОДА
	        |ИЗ
	        |	РегистрНакопления.ИсчисленныеСтраховыеВзносы КАК ИсчисленныеСтраховыеВзносы
	        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыЗастрахованныхФизическихЛиц.СрезПоследних(&КонецПериода, ) КАК СтатусыЗастрахованныхФизическихЛицСрезПоследних
	        |		ПО ИсчисленныеСтраховыеВзносы.ФизическоеЛицо = СтатусыЗастрахованныхФизическихЛицСрезПоследних.ФизическоеЛицо
	        |ГДЕ
	        |	ИсчисленныеСтраховыеВзносы.Период МЕЖДУ &НачалоПериода И &КонецПериода
	        |	И (СтатусыЗастрахованныхФизическихЛицСрезПоследних.ВидЗастрахованногоЛица <> ЗНАЧЕНИЕ(Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПребывающиеИностранцыНестрахуемые)
	        |			ИЛИ СтатусыЗастрахованныхФизическихЛицСрезПоследних.ВидЗастрахованногоЛица ЕСТЬ NULL )
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ИсчисленныеСтраховыеВзносы.ФизическоеЛицо.ФИО,
	        |	ИсчисленныеСтраховыеВзносы.ФизическоеЛицо.ИНН,
	        |	ИсчисленныеСтраховыеВзносы.ФизическоеЛицо.СтраховойНомерПФР,
	        |	НАЧАЛОПЕРИОДА(ИсчисленныеСтраховыеВзносы.Период, МЕСЯЦ),
	        |	СтатусыЗастрахованныхФизическихЛицСрезПоследних.ВидЗастрахованногоЛица
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	Данные2.ФИО,
	        |	Данные2.ИНН,
	        |	Данные2.СНИЛС,
	        |	Данные2.Период,
	        |	СУММА(Данные2.ФФОМС_СНачалаГода) КАК ФФОМС_СНачалаГода,
	        |	СУММА(Данные2.ФФОМС_ВТекущемМесяце) КАК ФФОМС_ВТекущемМесяце,
	        |	СУММА(Данные2.ДоходСНачалаГода) КАК ДоходСНачалаГода,
	        |	СУММА(Данные2.ДоходВТекущемМесяце) КАК ДоходВТекущемМесяце,
	        |	СУММА(Данные2.НеОблагаемаяБазаСНачалаГода) КАК НеОблагаемаяБазаСНачалаГода,
	        |	СУММА(Данные2.НеОблагаемаяБазаВТекущемМесяце) КАК НеОблагаемаяБазаВТекущемМесяце,
	        |	Данные2.ВидЗастрахованногоЛица
	        |ИЗ
	        |	(ВЫБРАТЬ
	        |		Данные.ФизическоеЛицоФИО КАК ФИО,
	        |		Данные.ФизическоеЛицоИНН КАК ИНН,
	        |		Данные.ФизическоеЛицоСтраховойНомерПФР КАК СНИЛС,
	        |		Данные.Период КАК Период,
	        |		СУММА(Данные.ФФОМС_СНачалаГода) КАК ФФОМС_СНачалаГода,
	        |		СУММА(Данные.ФФОМС_ВТекущемМесяце) КАК ФФОМС_ВТекущемМесяце,
	        |		NULL КАК ДоходСНачалаГода,
	        |		NULL КАК ДоходВТекущемМесяце,
	        |		0 КАК НеОблагаемаяБазаСНачалаГода,
	        |		0 КАК НеОблагаемаяБазаВТекущемМесяце,
	        |		Данные.ВидЗастрахованногоЛица КАК ВидЗастрахованногоЛица
	        |	ИЗ
	        |		(ВЫБРАТЬ
	        |			ВТ_ФОМС_С_НАЧАЛА_ГОДА.ФизическоеЛицоФИО КАК ФизическоеЛицоФИО,
	        |			ВТ_ФОМС_С_НАЧАЛА_ГОДА.ФизическоеЛицоИНН КАК ФизическоеЛицоИНН,
	        |			ВТ_ФОМС_С_НАЧАЛА_ГОДА.ФизическоеЛицоСтраховойНомерПФР КАК ФизическоеЛицоСтраховойНомерПФР,
	        |			ВТ_ФОМС_С_НАЧАЛА_ГОДА.ФФОМС КАК ФФОМС_СНачалаГода,
	        |			0 КАК ФФОМС_ВТекущемМесяце,
	        |			ВТ_ФОМС_С_НАЧАЛА_ГОДА.Период КАК Период,
	        |			ВТ_ФОМС_С_НАЧАЛА_ГОДА.ВидЗастрахованногоЛица КАК ВидЗастрахованногоЛица
	        |		ИЗ
	        |			ВТ_ФОМС_С_НАЧАЛА_ГОДА КАК ВТ_ФОМС_С_НАЧАЛА_ГОДА
	        |		
	        |		ОБЪЕДИНИТЬ ВСЕ
	        |		
	        |		ВЫБРАТЬ
	        |			ВТ_ФОМС_С_НАЧАЛА_ГОДА.ФизическоеЛицоФИО,
	        |			ВТ_ФОМС_С_НАЧАЛА_ГОДА.ФизическоеЛицоИНН,
	        |			ВТ_ФОМС_С_НАЧАЛА_ГОДА.ФизическоеЛицоСтраховойНомерПФР,
	        |			0,
	        |			ВТ_ФОМС_С_НАЧАЛА_ГОДА.ФФОМС,
	        |			ВТ_ФОМС_С_НАЧАЛА_ГОДА.Период,
	        |			ВТ_ФОМС_С_НАЧАЛА_ГОДА.ВидЗастрахованногоЛица
	        |		ИЗ
	        |			ВТ_ФОМС_С_НАЧАЛА_ГОДА КАК ВТ_ФОМС_С_НАЧАЛА_ГОДА
	        |		ГДЕ
	        |			ВТ_ФОМС_С_НАЧАЛА_ГОДА.Период МЕЖДУ НАЧАЛОПЕРИОДА(&КонецПериода, МЕСЯЦ) И &КонецПериода) КАК Данные
	        |	
	        |	СГРУППИРОВАТЬ ПО
	        |		Данные.Период,
	        |		Данные.ФизическоеЛицоФИО,
	        |		Данные.ФизическоеЛицоСтраховойНомерПФР,
	        |		Данные.ФизическоеЛицоИНН,
	        |		Данные.ВидЗастрахованногоЛица
	        |	
	        |	ОБЪЕДИНИТЬ ВСЕ
	        |	
	        |	ВЫБРАТЬ
	        |		ВТ_ДОХОДЫ_2.ФИО,
	        |		ВТ_ДОХОДЫ_2.ИНН,
	        |		ВТ_ДОХОДЫ_2.СНИЛС,
	        |		ВТ_ДОХОДЫ_2.Период,
	        |		NULL,
	        |		NULL,
	        |		ВТ_ДОХОДЫ_2.ДоходСНачалаГода,
	        |		ВТ_ДОХОДЫ_2.ДоходВТекущемМесяце,
	        |		0,
	        |		0,
	        |		ВТ_ДОХОДЫ_2.ВидЗастрахованногоЛица
	        |	ИЗ
	        |		ВТ_ДОХОДЫ_2 КАК ВТ_ДОХОДЫ_2
	        |	
	        |	ОБЪЕДИНИТЬ ВСЕ
	        |	
	        |	ВЫБРАТЬ
	        |		СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо.ФИО,
	        |		СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо.ИНН,
	        |		СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо.СтраховойНомерПФР,
	        |		СведенияОДоходахСтраховыеВзносы.Период,
	        |		0,
	        |		0,
	        |		0,
	        |		0,
	        |		СведенияОДоходахСтраховыеВзносы.Сумма,
	        |		0,
	        |		СтатусыЗастрахованныхФизическихЛицСрезПоследних.ВидЗастрахованногоЛица
	        |	ИЗ
	        |		РегистрНакопления.СведенияОДоходахСтраховыеВзносы КАК СведенияОДоходахСтраховыеВзносы
	        |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыЗастрахованныхФизическихЛиц.СрезПоследних(&КонецПериода, ) КАК СтатусыЗастрахованныхФизическихЛицСрезПоследних
	        |			ПО СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо = СтатусыЗастрахованныхФизическихЛицСрезПоследних.ФизическоеЛицо
	        |	ГДЕ
	        |		СведенияОДоходахСтраховыеВзносы.ВидДохода.ВходитВБазуФОМС = ЛОЖЬ
	        |		И СведенияОДоходахСтраховыеВзносы.Период МЕЖДУ &НачалоПериода И &КонецПериода
	        |		И СведенияОДоходахСтраховыеВзносы.Сумма <> 0
	        |		И (СтатусыЗастрахованныхФизическихЛицСрезПоследних.ВидЗастрахованногоЛица <> ЗНАЧЕНИЕ(Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПребывающиеИностранцыНестрахуемые)
	        |				ИЛИ СтатусыЗастрахованныхФизическихЛицСрезПоследних.ВидЗастрахованногоЛица ЕСТЬ NULL )
	        |	
	        |	ОБЪЕДИНИТЬ ВСЕ
	        |	
	        |	ВЫБРАТЬ
	        |		СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо.ФИО,
	        |		СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо.ИНН,
	        |		СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо.СтраховойНомерПФР,
	        |		СведенияОДоходахСтраховыеВзносы.Период,
	        |		0,
	        |		0,
	        |		0,
	        |		0,
	        |		0,
	        |		СведенияОДоходахСтраховыеВзносы.Сумма,
	        |		СтатусыЗастрахованныхФизическихЛицСрезПоследних.ВидЗастрахованногоЛица
	        |	ИЗ
	        |		РегистрНакопления.СведенияОДоходахСтраховыеВзносы КАК СведенияОДоходахСтраховыеВзносы
	        |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыЗастрахованныхФизическихЛиц.СрезПоследних(&КонецПериода, ) КАК СтатусыЗастрахованныхФизическихЛицСрезПоследних
	        |			ПО СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо = СтатусыЗастрахованныхФизическихЛицСрезПоследних.ФизическоеЛицо
	        |	ГДЕ
	        |		СведенияОДоходахСтраховыеВзносы.ВидДохода.ВходитВБазуФОМС = ЛОЖЬ
	        |		И СведенияОДоходахСтраховыеВзносы.Период МЕЖДУ НАЧАЛОПЕРИОДА(&КонецПериода, МЕСЯЦ) И &КонецПериода
	        |		И СведенияОДоходахСтраховыеВзносы.Сумма <> 0
	        |		И (СтатусыЗастрахованныхФизическихЛицСрезПоследних.ВидЗастрахованногоЛица <> ЗНАЧЕНИЕ(Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПребывающиеИностранцыНестрахуемые)
	        |				ИЛИ СтатусыЗастрахованныхФизическихЛицСрезПоследних.ВидЗастрахованногоЛица ЕСТЬ NULL )) КАК Данные2
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	Данные2.ФИО,
	        |	Данные2.ИНН,
	        |	Данные2.СНИЛС,
	        |	Данные2.Период,
	        |	Данные2.ВидЗастрахованногоЛица";	
КонецФункции

Функция ПолучитьИтоговуюТаблицу()
	
	МассивМесяцев 		= ПолучитьТаблицуМесяцев(НачалоПериода, КонецПериода);
	COM_Соединение 		= ППФ_Сервер.УстановитьCOMПодключениеСБазойДанных1С();
	
	Данные = Новый ТаблицаЗначений;
	Данные.Колонки.Добавить("ФИО", Новый ОписаниеТипов("Строка"));
	Данные.Колонки.Добавить("ИНН", Новый ОписаниеТипов("Строка"));
	Данные.Колонки.Добавить("СНИЛС", Новый ОписаниеТипов("Строка"));
	Данные.Колонки.Добавить("ФФОМС_СНачалаГода", Новый ОписаниеТипов("Число"));
	Данные.Колонки.Добавить("ФФОМС_ВТекущемМесяце", Новый ОписаниеТипов("Число"));
	Данные.Колонки.Добавить("ДоходСНачалаГода", Новый ОписаниеТипов("Число"));
	Данные.Колонки.Добавить("ДоходВТекущемМесяце", Новый ОписаниеТипов("Число"));
	Данные.Колонки.Добавить("ФФОМС_СНачалаГода_Ш", Новый ОписаниеТипов("Число"));
	Данные.Колонки.Добавить("ФФОМС_СНачалаГода_А", Новый ОписаниеТипов("Число"));
	Данные.Колонки.Добавить("ФФОМС_ВТекущемМесяце_Ш", Новый ОписаниеТипов("Число"));
	Данные.Колонки.Добавить("ФФОМС_ВТекущемМесяце_А", Новый ОписаниеТипов("Число"));
	Данные.Колонки.Добавить("НеОблагаемаяБазаСНачалаГода_Ш", Новый ОписаниеТипов("Число"));
	Данные.Колонки.Добавить("НеОблагаемаяБазаВТекущемМесяце_Ш", Новый ОписаниеТипов("Число"));
	Данные.Колонки.Добавить("НеОблагаемаяБазаСНачалаГода_А", Новый ОписаниеТипов("Число"));
	Данные.Колонки.Добавить("НеОблагаемаяБазаВТекущемМесяце_А", Новый ОписаниеТипов("Число"));
	
	КоличествоМесяцев = МассивМесяцев.Количество();
	ПоследнийМесяц = МассивМесяцев[КоличествоМесяцев - 1];
		
	Для Каждого ВыбранныйМесяц Из МассивМесяцев Цикл		
		
		ФОМСШтатныйСотрудник = ПолучитьФОМСПоШтатнымСотрудникам(ВыбранныйМесяц);
		ФОМСАгенты = ПолучитьФОМСПоАгентам(COM_Соединение, ВыбранныйМесяц);
		
		ФОМСШтатныйСотрудник_Отбор = ФОМСШтатныйСотрудник.Скопировать(Новый Структура("Период",ВыбранныйМесяц));
		ФОМСАгенты_Отбор = ФОМСАгенты.Скопировать(Новый Структура("Период",ВыбранныйМесяц));
		
		Для Каждого Строка1 Из ФОМСШтатныйСотрудник_Отбор Цикл
			НоваяСтрока = Данные.Добавить();
			НоваяСтрока.ФИО 	= Строка1.ФИО;
			НоваяСтрока.ИНН 	= Строка1.ИНН;
			НоваяСтрока.СНИЛС 	= Строка1.СНИЛС;
			НоваяСтрока.ФФОМС_ВТекущемМесяце 	= Строка1.ФФОМС_ВТекущемМесяце;
			НоваяСтрока.ФФОМС_ВТекущемМесяце_Ш 	= Строка1.ФФОМС_ВТекущемМесяце;
			НоваяСтрока.ДоходВТекущемМесяце 	= Строка1.ДоходВТекущемМесяце;
			НоваяСтрока.НеОблагаемаяБазаВТекущемМесяце_Ш = Строка1.НеОблагаемаяБазаВТекущемМесяце;				
			Если ПоследнийМесяц = ВыбранныйМесяц Тогда
				НоваяСтрока.ФФОМС_СНачалаГода				= Строка1.ФФОМС_СНачалаГода;
				НоваяСтрока.ДоходСНачалаГода				= Строка1.ДоходСНачалаГода;
				НоваяСтрока.ФФОМС_СНачалаГода_Ш				= Строка1.ФФОМС_СНачалаГода;
				НоваяСтрока.НеОблагаемаяБазаСНачалаГода_Ш	= Строка1.НеОблагаемаяБазаСНачалаГода;					
			КонецЕсли;
		КонецЦикла; 
		
		Для Каждого Строка2 Из ФОМСАгенты_Отбор Цикл
			НоваяСтрока = Данные.Добавить();
			НоваяСтрока.ФИО 	= Строка2.ФИО;
			НоваяСтрока.ИНН 	= Строка2.ИНН;
			НоваяСтрока.СНИЛС 	= Строка2.СНИЛС;
			НоваяСтрока.ФФОМС_ВТекущемМесяце 	= Строка2.ФФОМС_ВТекущемМесяце;
			НоваяСтрока.ФФОМС_ВТекущемМесяце_А 	= Строка2.ФФОМС_ВТекущемМесяце;
			НоваяСтрока.ДоходВТекущемМесяце 	= Строка2.ДоходВТекущемМесяце;
			НоваяСтрока.НеОблагаемаяБазаВТекущемМесяце_А = Строка2.НеОблагаемаяБазаВТекущемМесяце;				
			Если ПоследнийМесяц = ВыбранныйМесяц Тогда
				НоваяСтрока.ФФОМС_СНачалаГода				= Строка2.ФФОМС_СНачалаГода;
				НоваяСтрока.ДоходСНачалаГода				= Строка2.ДоходСНачалаГода;
				НоваяСтрока.ФФОМС_СНачалаГода_А				= Строка2.ФФОМС_СНачалаГода;
				НоваяСтрока.НеОблагаемаяБазаСНачалаГода_А	= Строка2.НеОблагаемаяБазаСНачалаГода;					
			КонецЕсли;
		КонецЦикла;	
	КонецЦикла;
	
	Данные.Свернуть("ФИО,СНИЛС,ИНН","НеОблагаемаяБазаСНачалаГода_Ш,НеОблагаемаяБазаВТекущемМесяце_Ш,НеОблагаемаяБазаСНачалаГода_А,НеОблагаемаяБазаВТекущемМесяце_А,ДоходСНачалаГода,ДоходВТекущемМесяце,ФФОМС_СНачалаГода,ФФОМС_ВТекущемМесяце,ФФОМС_СНачалаГода_Ш,ФФОМС_СНачалаГода_А,ФФОМС_ВТекущемМесяце_Ш,ФФОМС_ВТекущемМесяце_А");
	
	Возврат Данные;
	
КонецФункции // ПолучитьИтоговуюТаблицу()

Функция ПолучитьФОМСПоШтатнымСотрудникам(ВыбранныйМесяц)
	Запрос = Новый Запрос;
	Запрос.Текст = ПолучитьТекстЗапроса();
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоГода(ВыбранныйМесяц));
	Запрос.УстановитьПараметр("КонецПериода",  КонецМесяца(ВыбранныйМесяц));
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		Возврат Результат.Выгрузить();
	КонецЕсли; 	
КонецФункции

Функция ПолучитьФОМСПоАгентам(COM_Соединение, ВыбранныйМесяц)
	
	Запрос = COM_Соединение.NewObject("Запрос");
	Запрос.Текст = ПолучитьТекстЗапроса();	
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоГода(ВыбранныйМесяц));
	Запрос.УстановитьПараметр("КонецПериода",  КонецМесяца(ВыбранныйМесяц));	
		
	Возврат ЗначениеИзСтрокиВнутр(COM_Соединение.ЗначениеВСтрокуВнутр(Запрос.Выполнить().Выгрузить()));
	
КонецФункции

Функция ПолучитьТаблицуМесяцев(НачалоВыбранныйМесяц, КонецВыбранныйМесяц)
	
	МассивДат = Новый Массив;
	
    Запрос = Новый Запрос;
    Запрос.Текст = 
	"ВЫБРАТЬ
    |   НАЧАЛОПЕРИОДА(&НачДата, День) КАК Период
    |УПОРЯДОЧИТЬ ПО
    |   Период
    |ИТОГИ ПО
    |   Период ПЕРИОДАМИ(МЕСЯЦ, &НачДата, &КонДата)";
	
	Запрос.УстановитьПараметр("НачДата", НачалоМесяца(НачалоВыбранныйМесяц));
    Запрос.УстановитьПараметр("КонДата", КонецМесяца(КонецВыбранныйМесяц));

    Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Период", "ВСЕ");
	Пока Выборка.Следующий() Цикл
		МассивДат.Добавить(Выборка.Период);
    КонецЦикла;

    Возврат МассивДат;

КонецФункции 

#КонецОбласти 
