#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	СхемаКомпоновкиДанных = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	Настройки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);
	
	ИтоговаяТаблица = ПолучитьИтоговуюТаблицу();
	
	ВнешнийНаборДанных = Новый Структура;
	ВнешнийНаборДанных.Вставить("ИтоговаяТаблица", ИтоговаяТаблица);
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешнийНаборДанных, ДанныеРасшифровки);
	
	ДокументРезультат.Очистить();
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	ДокументРезультат.ФиксацияСлева = 3;

	
КонецПроцедуры

#КонецОбласти

#Область СведенияОВнешнейОбработке

Функция СведенияОВнешнейОбработке() Экспорт
	ПараметрыРегистрации = Новый Структура;
	ПараметрыРегистрации.Вставить("Вид", "ДополнительныйОтчет");
	ПараметрыРегистрации.Вставить("Наименование", "Отчет по взносам в ПФР (за период)");
	ПараметрыРегистрации.Вставить("Версия", "1.0");
	ПараметрыРегистрации.Вставить("Информация", "Отчет по взносам в ПФР (за период)");
	ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);
	
	Команды = ТаблицаКоманд();
	ДобавитьКоманду(Команды, "Отчет по взносам в ПФР (за период)", "ОтчетПоВзносамВПФР_за_период", "ОткрытиеФормы", Ложь, "");
	
	ПараметрыРегистрации.Вставить("Команды", Команды);
	
	Возврат ПараметрыРегистрации;
КонецФункции

Функция ТаблицаКоманд()
	Команды = Новый ТаблицаЗначений;
	Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));
	Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
	Возврат Команды;
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
	НоваяКоманда = ТаблицаКоманд.Добавить();
	НоваяКоманда.Представление = Представление;
	НоваяКоманда.Идентификатор = Идентификатор;
	НоваяКоманда.Использование = Использование;
	НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
	НоваяКоманда.Модификатор = Модификатор;
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТекстЗапроса()
	
	Возврат "ВЫБРАТЬ РАЗЛИЧНЫЕ
	        |	Начисления.Ссылка КАК Ссылка
	        |ПОМЕСТИТЬ ВТ_ВИДЫ_НАЧИСЛЕНИЙ
	        |ИЗ
	        |	ПланВидовРасчета.Начисления КАК Начисления
	        |ГДЕ
	        |	Начисления.Код = ""МТПМР""
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	Ссылка
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ИсчисленныеСтраховыеВзносы.ФизическоеЛицо КАК ФизическоеЛицо,
	        |	СУММА(ИсчисленныеСтраховыеВзносы.ПФРДоПредельнойВеличины + ИсчисленныеСтраховыеВзносы.ПФРСПревышения) КАК ИсчисленноВзносовВПФР,
	        |	НАЧАЛОПЕРИОДА(ИсчисленныеСтраховыеВзносы.Период, МЕСЯЦ) КАК Период
	        |ПОМЕСТИТЬ ВТ_ИСЧИСЛЕННЫЕ_ВЗНОСЫ
	        |ИЗ
	        |	РегистрНакопления.ИсчисленныеСтраховыеВзносы КАК ИсчисленныеСтраховыеВзносы
	        |ГДЕ
	        |	ИсчисленныеСтраховыеВзносы.Период МЕЖДУ &НачалоПериода И &КонецПериода
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ИсчисленныеСтраховыеВзносы.ФизическоеЛицо,
	        |	НАЧАЛОПЕРИОДА(ИсчисленныеСтраховыеВзносы.Период, МЕСЯЦ)
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	ФизическоеЛицо
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо.Наименование КАК ФИО,
	        |	ВЫРАЗИТЬ(СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо.СтраховойНомерПФР КАК СТРОКА(20)) КАК СНИЛС,
	        |	СУММА(ВЫБОР
	        |			КОГДА СведенияОДоходахСтраховыеВзносы.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.Матпомощь)
	        |				ТОГДА СведенияОДоходахСтраховыеВзносы.Сумма - СведенияОДоходахСтраховыеВзносы.Скидка
	        |			ИНАЧЕ СведенияОДоходахСтраховыеВзносы.Сумма
	        |		КОНЕЦ) КАК Доход,
	        |	СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо КАК ФизическоеЛицо,
	        |	НАЧАЛОПЕРИОДА(СведенияОДоходахСтраховыеВзносы.Период, МЕСЯЦ) КАК Период
	        |ПОМЕСТИТЬ ВТ_ДОХОДЫ
	        |ИЗ
	        |	РегистрНакопления.СведенияОДоходахСтраховыеВзносы КАК СведенияОДоходахСтраховыеВзносы
	        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыЗастрахованныхФизическихЛиц.СрезПоследних(&КонецПериода, ) КАК СтатусыЗастрахованныхФизическихЛицСрезПоследних
	        |		ПО СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо = СтатусыЗастрахованныхФизическихЛицСрезПоследних.ФизическоеЛицо
	        |ГДЕ
	        |	СведенияОДоходахСтраховыеВзносы.ВидДохода.ВходитВБазуПФР = ИСТИНА
	        |	И СведенияОДоходахСтраховыеВзносы.Период МЕЖДУ &НачалоПериода И &КонецПериода
	        |	И СведенияОДоходахСтраховыеВзносы.Сумма <> 0
	        |	И (СтатусыЗастрахованныхФизическихЛицСрезПоследних.ВидЗастрахованногоЛица <> ЗНАЧЕНИЕ(Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПребывающиеИностранцыНестрахуемые)
	        |			ИЛИ СтатусыЗастрахованныхФизическихЛицСрезПоследних.ВидЗастрахованногоЛица ЕСТЬ NULL )
	        |	И НЕ(СведенияОДоходахСтраховыеВзносы.Начисление В
	        |					(ВЫБРАТЬ
	        |						ВТ_ВИДЫ_НАЧИСЛЕНИЙ.Ссылка
	        |					ИЗ
	        |						ВТ_ВИДЫ_НАЧИСЛЕНИЙ КАК ВТ_ВИДЫ_НАЧИСЛЕНИЙ)
	        |				И СведенияОДоходахСтраховыеВзносы.Сумма < 51000)
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо.Наименование,
	        |	ВЫРАЗИТЬ(СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо.СтраховойНомерПФР КАК СТРОКА(20)),
	        |	СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо,
	        |	НАЧАЛОПЕРИОДА(СведенияОДоходахСтраховыеВзносы.Период, МЕСЯЦ)
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	ФизическоеЛицо
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ВТ_ДОХОДЫ.ФИО,
	        |	ВТ_ДОХОДЫ.СНИЛС,
	        |	ВТ_ДОХОДЫ.Доход,
	        |	ВТ_ИСЧИСЛЕННЫЕ_ВЗНОСЫ.ИсчисленноВзносовВПФР,
	        |	НАЧАЛОПЕРИОДА(ВТ_ДОХОДЫ.Период, МЕСЯЦ) КАК Период
	        |ПОМЕСТИТЬ ВТ_ДОХОДЫ_И_ИСЧИСЛЕННЫЕ_ВЗНОСЫ
	        |ИЗ
	        |	ВТ_ДОХОДЫ КАК ВТ_ДОХОДЫ
	        |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИСЧИСЛЕННЫЕ_ВЗНОСЫ КАК ВТ_ИСЧИСЛЕННЫЕ_ВЗНОСЫ
	        |		ПО ВТ_ДОХОДЫ.ФизическоеЛицо = ВТ_ИСЧИСЛЕННЫЕ_ВЗНОСЫ.ФизическоеЛицо
	        |			И ВТ_ДОХОДЫ.Период = ВТ_ИСЧИСЛЕННЫЕ_ВЗНОСЫ.Период
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	Период
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ВТ_ДОХОДЫ_И_ИСЧИСЛЕННЫЕ_ВЗНОСЫ.ФИО,
	        |	ВТ_ДОХОДЫ_И_ИСЧИСЛЕННЫЕ_ВЗНОСЫ.СНИЛС,
	        |	СУММА(ВТ_ДОХОДЫ_И_ИСЧИСЛЕННЫЕ_ВЗНОСЫ.Доход) КАК ДоходСНачалаГода,
	        |	СУММА(ВТ_ДОХОДЫ_И_ИСЧИСЛЕННЫЕ_ВЗНОСЫ.ИсчисленноВзносовВПФР) КАК ИсчисленноВзносовВПФРСНачалаГода
	        |ПОМЕСТИТЬ ВТ_ДОХОДЫ_И_ИСЧИСЛЕННЫЕ_ВЗНОСЫ_ДО_ТЕКУЩЕГО_МЕСЯЦА
	        |ИЗ
	        |	ВТ_ДОХОДЫ_И_ИСЧИСЛЕННЫЕ_ВЗНОСЫ КАК ВТ_ДОХОДЫ_И_ИСЧИСЛЕННЫЕ_ВЗНОСЫ
	        |ГДЕ
	        |	ВТ_ДОХОДЫ_И_ИСЧИСЛЕННЫЕ_ВЗНОСЫ.Период < &ТекущийПериод
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ВТ_ДОХОДЫ_И_ИСЧИСЛЕННЫЕ_ВЗНОСЫ.ФИО,
	        |	ВТ_ДОХОДЫ_И_ИСЧИСЛЕННЫЕ_ВЗНОСЫ.СНИЛС
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ВТ_ДОХОДЫ_И_ИСЧИСЛЕННЫЕ_ВЗНОСЫ.ФИО,
	        |	ВТ_ДОХОДЫ_И_ИСЧИСЛЕННЫЕ_ВЗНОСЫ.СНИЛС,
	        |	СУММА(ВТ_ДОХОДЫ_И_ИСЧИСЛЕННЫЕ_ВЗНОСЫ.Доход) КАК ДоходВТекущемМесяце,
	        |	СУММА(ВТ_ДОХОДЫ_И_ИСЧИСЛЕННЫЕ_ВЗНОСЫ.ИсчисленноВзносовВПФР) КАК ИсчисленноВзносовВПФРВТекущемМесяце
	        |ПОМЕСТИТЬ ВТ_ДОХОДЫ_И_ИСЧИСЛЕННЫЕ_ВЗНОСЫ_В_ТЕКУЩЕМ_МЕСЯЦЕ
	        |ИЗ
	        |	ВТ_ДОХОДЫ_И_ИСЧИСЛЕННЫЕ_ВЗНОСЫ КАК ВТ_ДОХОДЫ_И_ИСЧИСЛЕННЫЕ_ВЗНОСЫ
	        |ГДЕ
	        |	ВТ_ДОХОДЫ_И_ИСЧИСЛЕННЫЕ_ВЗНОСЫ.Период = &ТекущийПериод
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ВТ_ДОХОДЫ_И_ИСЧИСЛЕННЫЕ_ВЗНОСЫ.ФИО,
	        |	ВТ_ДОХОДЫ_И_ИСЧИСЛЕННЫЕ_ВЗНОСЫ.СНИЛС
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	Данные.ФИО,
	        |	Данные.СНИЛС,
	        |	СУММА(Данные.ДоходВТекущемМесяце) КАК ДоходВТекущемМесяце,
	        |	СУММА(Данные.ИсчисленноВзносовВПФРВТекущемМесяце) КАК ИсчисленноВзносовВПФРВТекущемМесяце,
	        |	СУММА(Данные.ДоходСНачалаГода) КАК ДоходСНачалаГода,
	        |	СУММА(Данные.ИсчисленноВзносовВПФРСНачалаГода) КАК ИсчисленноВзносовВПФРСНачалаГода,
	        |	0 КАК ПФР_22,
	        |	0 КАК ПФР_10,
	        |	0 КАК ПФР_22_10,
	        |	СУММА(Данные.НеОблагаемаяБазаСНачалаГода) КАК НеОблагаемаяБазаСНачалаГода,
	        |	СУММА(Данные.НеОблагаемаяБазаВТекущемМесяце) КАК НеОблагаемаяБазаВТекущемМесяце
	        |ИЗ
	        |	(ВЫБРАТЬ
	        |		ВТ_НЕ_ВКЛЮЧАЯ_ТЕКУЩИЙ_ПЕРИОД.ФИО КАК ФИО,
	        |		ВТ_НЕ_ВКЛЮЧАЯ_ТЕКУЩИЙ_ПЕРИОД.СНИЛС КАК СНИЛС,
	        |		NULL КАК ДоходВТекущемМесяце,
	        |		NULL КАК ИсчисленноВзносовВПФРВТекущемМесяце,
	        |		ВТ_НЕ_ВКЛЮЧАЯ_ТЕКУЩИЙ_ПЕРИОД.ДоходСНачалаГода КАК ДоходСНачалаГода,
	        |		ВТ_НЕ_ВКЛЮЧАЯ_ТЕКУЩИЙ_ПЕРИОД.ИсчисленноВзносовВПФРСНачалаГода КАК ИсчисленноВзносовВПФРСНачалаГода,
	        |		0 КАК НеОблагаемаяБазаСНачалаГода,
	        |		0 КАК НеОблагаемаяБазаВТекущемМесяце
	        |	ИЗ
	        |		ВТ_ДОХОДЫ_И_ИСЧИСЛЕННЫЕ_ВЗНОСЫ_ДО_ТЕКУЩЕГО_МЕСЯЦА КАК ВТ_НЕ_ВКЛЮЧАЯ_ТЕКУЩИЙ_ПЕРИОД
	        |	
	        |	ОБЪЕДИНИТЬ ВСЕ
	        |	
	        |	ВЫБРАТЬ
	        |		ВТ_ТЕКУЩИЙ_МЕСЯЦ.ФИО,
	        |		ВТ_ТЕКУЩИЙ_МЕСЯЦ.СНИЛС,
	        |		ВТ_ТЕКУЩИЙ_МЕСЯЦ.ДоходВТекущемМесяце,
	        |		ВТ_ТЕКУЩИЙ_МЕСЯЦ.ИсчисленноВзносовВПФРВТекущемМесяце,
	        |		0,
	        |		0,
	        |		0,
	        |		0
	        |	ИЗ
	        |		ВТ_ДОХОДЫ_И_ИСЧИСЛЕННЫЕ_ВЗНОСЫ_В_ТЕКУЩЕМ_МЕСЯЦЕ КАК ВТ_ТЕКУЩИЙ_МЕСЯЦ
	        |	
	        |	ОБЪЕДИНИТЬ ВСЕ
	        |	
	        |	ВЫБРАТЬ
	        |		СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо.ФИО,
	        |		СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо.СтраховойНомерПФР,
	        |		0,
	        |		0,
	        |		0,
	        |		0,
	        |		СведенияОДоходахСтраховыеВзносы.Сумма,
	        |		0
	        |	ИЗ
	        |		РегистрНакопления.СведенияОДоходахСтраховыеВзносы КАК СведенияОДоходахСтраховыеВзносы
	        |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыЗастрахованныхФизическихЛиц.СрезПоследних(&КонецПериода, ) КАК СтатусыЗастрахованныхФизическихЛицСрезПоследних
	        |			ПО СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо = СтатусыЗастрахованныхФизическихЛицСрезПоследних.ФизическоеЛицо
	        |	ГДЕ
	        |		СведенияОДоходахСтраховыеВзносы.ВидДохода.ВходитВБазуПФР = ЛОЖЬ
	        |		И СведенияОДоходахСтраховыеВзносы.Период МЕЖДУ &НачалоПериода И &КонецПериода
	        |		И СведенияОДоходахСтраховыеВзносы.Сумма <> 0
	        |		И (СтатусыЗастрахованныхФизическихЛицСрезПоследних.ВидЗастрахованногоЛица <> ЗНАЧЕНИЕ(Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПребывающиеИностранцыНестрахуемые)
	        |				ИЛИ СтатусыЗастрахованныхФизическихЛицСрезПоследних.ВидЗастрахованногоЛица ЕСТЬ NULL )
	        |	
	        |	ОБЪЕДИНИТЬ ВСЕ
	        |	
	        |	ВЫБРАТЬ
	        |		СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо.ФИО,
	        |		СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо.СтраховойНомерПФР,
	        |		0,
	        |		0,
	        |		0,
	        |		0,
	        |		0,
	        |		СведенияОДоходахСтраховыеВзносы.Сумма
	        |	ИЗ
	        |		РегистрНакопления.СведенияОДоходахСтраховыеВзносы КАК СведенияОДоходахСтраховыеВзносы
	        |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыЗастрахованныхФизическихЛиц.СрезПоследних(&КонецПериода, ) КАК СтатусыЗастрахованныхФизическихЛицСрезПоследних
	        |			ПО СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо = СтатусыЗастрахованныхФизическихЛицСрезПоследних.ФизическоеЛицо
	        |	ГДЕ
	        |		СведенияОДоходахСтраховыеВзносы.ВидДохода.ВходитВБазуПФР = ЛОЖЬ
	        |		И СведенияОДоходахСтраховыеВзносы.Период МЕЖДУ НАЧАЛОПЕРИОДА(&КонецПериода, МЕСЯЦ) И &КонецПериода
	        |		И СведенияОДоходахСтраховыеВзносы.Сумма <> 0
	        |		И (СтатусыЗастрахованныхФизическихЛицСрезПоследних.ВидЗастрахованногоЛица <> ЗНАЧЕНИЕ(Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПребывающиеИностранцыНестрахуемые)
	        |				ИЛИ СтатусыЗастрахованныхФизическихЛицСрезПоследних.ВидЗастрахованногоЛица ЕСТЬ NULL )
	        |	
	        |	ОБЪЕДИНИТЬ ВСЕ
	        |	
	        |	ВЫБРАТЬ
	        |		СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо.ФИО,
	        |		СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо.СтраховойНомерПФР,
	        |		0,
	        |		0,
	        |		0,
	        |		0,
	        |		0,
	        |		СведенияОДоходахСтраховыеВзносы.Скидка
	        |	ИЗ
	        |		РегистрНакопления.СведенияОДоходахСтраховыеВзносы КАК СведенияОДоходахСтраховыеВзносы
	        |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыЗастрахованныхФизическихЛиц.СрезПоследних(&КонецПериода, ) КАК СтатусыЗастрахованныхФизическихЛицСрезПоследних
	        |			ПО СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо = СтатусыЗастрахованныхФизическихЛицСрезПоследних.ФизическоеЛицо
	        |	ГДЕ
	        |		СведенияОДоходахСтраховыеВзносы.Период МЕЖДУ НАЧАЛОПЕРИОДА(&КонецПериода, МЕСЯЦ) И &КонецПериода
	        |		И СведенияОДоходахСтраховыеВзносы.Сумма <> 0
	        |		И (СведенияОДоходахСтраховыеВзносы.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.МатпомощьПриРожденииРебенка)
	        |				ИЛИ СведенияОДоходахСтраховыеВзносы.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.Матпомощь))
	        |		И (СтатусыЗастрахованныхФизическихЛицСрезПоследних.ВидЗастрахованногоЛица <> ЗНАЧЕНИЕ(Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПребывающиеИностранцыНестрахуемые)
	        |				ИЛИ СтатусыЗастрахованныхФизическихЛицСрезПоследних.ВидЗастрахованногоЛица ЕСТЬ NULL )
	        |	
	        |	ОБЪЕДИНИТЬ ВСЕ
	        |	
	        |	ВЫБРАТЬ
	        |		СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо.ФИО,
	        |		СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо.СтраховойНомерПФР,
	        |		0,
	        |		0,
	        |		0,
	        |		0,
	        |		СведенияОДоходахСтраховыеВзносы.Скидка,
	        |		0
	        |	ИЗ
	        |		РегистрНакопления.СведенияОДоходахСтраховыеВзносы КАК СведенияОДоходахСтраховыеВзносы
	        |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыЗастрахованныхФизическихЛиц.СрезПоследних(&КонецПериода, ) КАК СтатусыЗастрахованныхФизическихЛицСрезПоследних
	        |			ПО СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо = СтатусыЗастрахованныхФизическихЛицСрезПоследних.ФизическоеЛицо
	        |	ГДЕ
	        |		СведенияОДоходахСтраховыеВзносы.Период МЕЖДУ &НачалоПериода И &КонецПериода
	        |		И СведенияОДоходахСтраховыеВзносы.Сумма <> 0
	        |		И (СведенияОДоходахСтраховыеВзносы.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.МатпомощьПриРожденииРебенка)
	        |				ИЛИ СведенияОДоходахСтраховыеВзносы.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.Матпомощь))
	        |		И (СтатусыЗастрахованныхФизическихЛицСрезПоследних.ВидЗастрахованногоЛица <> ЗНАЧЕНИЕ(Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПребывающиеИностранцыНестрахуемые)
	        |				ИЛИ СтатусыЗастрахованныхФизическихЛицСрезПоследних.ВидЗастрахованногоЛица ЕСТЬ NULL )) КАК Данные
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	Данные.ФИО,
	        |	Данные.СНИЛС";
	
КонецФункции // ПолучитьТекстЗапроса()

Функция ПолучитьДоходыАгентов(НачалоПериода, ВыбранныйМесяц, COM_Соединение)
	
	COM_Соединение = ППФ_Сервер.УстановитьCOMПодключениеСБазойДанных1С();
	
	Запрос = COM_Соединение.NewObject("Запрос");
	Запрос.Текст = ПолучитьТекстЗапроса();	
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(НачалоПериода));
	Запрос.УстановитьПараметр("КонецПериода",  КонецМесяца(ВыбранныйМесяц));	
	Запрос.УстановитьПараметр("ТекущийПериод", НачалоМесяца(ВыбранныйМесяц));
		
	Возврат ЗначениеИзСтрокиВнутр(COM_Соединение.ЗначениеВСтрокуВнутр(Запрос.Выполнить().Выгрузить()));
	
КонецФункции // ПолучитьДоходыАгентов()

Функция ПолучитьДоходыШтат(НачалоПериода, ВыбранныйМесяц)
	
	Запрос = Новый Запрос;
	Запрос.Текст = ПолучитьТекстЗапроса();
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(НачалоПериода));
	Запрос.УстановитьПараметр("КонецПериода",  КонецМесяца(ВыбранныйМесяц));	
	Запрос.УстановитьПараметр("ТекущийПериод", НачалоМесяца(ВыбранныйМесяц));
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		Возврат Результат.Выгрузить();
		
	КонецЕсли;	
	
КонецФункции

Функция ПолучитьДоходы(НачалоПериода,ВыбранныйМесяц, СуммаРегрессаПФР, COM_Соединение)

	ДоходыШтат = ПолучитьДоходыШтат(НачалоПериода, ВыбранныйМесяц);
	ДоходыАгентов = ПолучитьДоходыАгентов(НачалоПериода, ВыбранныйМесяц, COM_Соединение);
	
	Доходы = Новый ТаблицаЗначений;
	Доходы.Колонки.Добавить("ФИО", Новый ОписаниеТипов("Строка"));
	Доходы.Колонки.Добавить("СНИЛС", Новый ОписаниеТипов(Новый КвалификаторыСтроки(20)));
	
	Доходы.Колонки.Добавить("ДоходВТекущемМесяцеШтат", Новый ОписаниеТипов("Число"));
	Доходы.Колонки.Добавить("ИсчисленноВзносовВПФРВТекущемМесяцеШтат", Новый ОписаниеТипов("Число"));
	Доходы.Колонки.Добавить("ДоходСНачалаГодаШтат", Новый ОписаниеТипов("Число"));
	Доходы.Колонки.Добавить("ИсчисленноВзносовВПФРСНачалаГодаШтат", Новый ОписаниеТипов("Число"));
	Доходы.Колонки.Добавить("НеОблагаемаяБазаВТекущемМесяцеШтат", Новый ОписаниеТипов("Число"));
	Доходы.Колонки.Добавить("НеОблагаемаяБазаСНачалаГодаШтат", Новый ОписаниеТипов("Число"));
	
	Доходы.Колонки.Добавить("ДоходВТекущемМесяцеАгент", Новый ОписаниеТипов("Число"));
	Доходы.Колонки.Добавить("ИсчисленноВзносовВПФРВТекущемМесяцеАгент", Новый ОписаниеТипов("Число"));
	Доходы.Колонки.Добавить("ДоходСНачалаГодаАгент", Новый ОписаниеТипов("Число"));
	Доходы.Колонки.Добавить("ИсчисленноВзносовВПФРСНачалаГодаАгент", Новый ОписаниеТипов("Число"));
	Доходы.Колонки.Добавить("НеОблагаемаяБазаВТекущемМесяцеАгент", Новый ОписаниеТипов("Число"));
	Доходы.Колонки.Добавить("НеОблагаемаяБазаСНачалаГодаАгент", Новый ОписаниеТипов("Число"));
				
	Для Каждого СтрокаДоходыШтат Из ДоходыШтат Цикл
		
		НоваяСтрока = Доходы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДоходыШтат);
		
		НоваяСтрока.ДоходВТекущемМесяцеШтат 					= СтрокаДоходыШтат.ДоходВТекущемМесяце;
		НоваяСтрока.ИсчисленноВзносовВПФРВТекущемМесяцеШтат 	= СтрокаДоходыШтат.ИсчисленноВзносовВПФРВТекущемМесяце;
		НоваяСтрока.ДоходСНачалаГодаШтат						= СтрокаДоходыШтат.ДоходСНачалаГода;
		НоваяСтрока.ИсчисленноВзносовВПФРСНачалаГодаШтат       	= СтрокаДоходыШтат.ИсчисленноВзносовВПФРСНачалаГода;
		НоваяСтрока.НеОблагаемаяБазаВТекущемМесяцеШтат			= СтрокаДоходыШтат.НеОблагаемаяБазаВТекущемМесяце;
		НоваяСтрока.НеОблагаемаяБазаСНачалаГодаШтат				= СтрокаДоходыШтат.НеОблагаемаяБазаСНачалаГода;
		
	КонецЦикла; 
	
	Для Каждого СтрокаДоходыАгентов Из ДоходыАгентов Цикл
		
		НоваяСтрока = Доходы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДоходыАгентов);
		
		НоваяСтрока.ДоходВТекущемМесяцеАгент 					= СтрокаДоходыАгентов.ДоходВТекущемМесяце;
		НоваяСтрока.ИсчисленноВзносовВПФРВТекущемМесяцеАгент 	= СтрокаДоходыАгентов.ИсчисленноВзносовВПФРВТекущемМесяце;
		НоваяСтрока.ДоходСНачалаГодаАгент						= СтрокаДоходыАгентов.ДоходСНачалаГода;
		НоваяСтрока.ИсчисленноВзносовВПФРСНачалаГодаАгент      	= СтрокаДоходыАгентов.ИсчисленноВзносовВПФРСНачалаГода;
		НоваяСтрока.НеОблагаемаяБазаВТекущемМесяцеАгент			= СтрокаДоходыАгентов.НеОблагаемаяБазаВТекущемМесяце;
		НоваяСтрока.НеОблагаемаяБазаСНачалаГодаАгент			= СтрокаДоходыАгентов.НеОблагаемаяБазаСНачалаГода;
		
	КонецЦикла;
	
	Доходы.Свернуть("ФИО,СНИЛС","НеОблагаемаяБазаВТекущемМесяцеАгент,НеОблагаемаяБазаСНачалаГодаАгент,НеОблагаемаяБазаВТекущемМесяцеШтат,НеОблагаемаяБазаСНачалаГодаШтат,ДоходВТекущемМесяцеШтат,ИсчисленноВзносовВПФРВТекущемМесяцеШтат,ДоходСНачалаГодаШтат,ИсчисленноВзносовВПФРСНачалаГодаШтат,ДоходВТекущемМесяцеАгент,ИсчисленноВзносовВПФРВТекущемМесяцеАгент,ДоходСНачалаГодаАгент,ИсчисленноВзносовВПФРСНачалаГодаАгент");
	
	Доходы.Колонки.Добавить("НачисленноВзносовВПФРВТекущемМесяце_22", Новый ОписаниеТипов("Число"));
	Доходы.Колонки.Добавить("НачисленноВзносовВПФРВТекущемМесяце_10", Новый ОписаниеТипов("Число"));
	Доходы.Колонки.Добавить("НачисленноВзносовВПФРВТекущемМесяце_2210", Новый ОписаниеТипов("Число"));
	
	Доходы.Колонки.Добавить("НачисленноВзносовВПФРВТекущемМесяце_22_Штат", Новый ОписаниеТипов("Число"));
	Доходы.Колонки.Добавить("НачисленноВзносовВПФРВТекущемМесяце_10_Штат", Новый ОписаниеТипов("Число"));
	Доходы.Колонки.Добавить("НачисленноВзносовВПФРВТекущемМесяце_2210_Штат", Новый ОписаниеТипов("Число"));
	
	Доходы.Колонки.Добавить("НачисленноВзносовВПФРВТекущемМесяце_22_Агент", Новый ОписаниеТипов("Число"));
	Доходы.Колонки.Добавить("НачисленноВзносовВПФРВТекущемМесяце_10_Агент", Новый ОписаниеТипов("Число"));
	Доходы.Колонки.Добавить("НачисленноВзносовВПФРВТекущемМесяце_2210_Агент", Новый ОписаниеТипов("Число"));
		
	Для Каждого СтрокаДоходы Из Доходы Цикл
		
		РасчитаныеВзносы = РасчитатьВзносыПоШтатномуСотрудникуИАгенту(СтрокаДоходы.ДоходСНачалаГодаШтат, 
																		СтрокаДоходы.ДоходСНачалаГодаАгент, 
																			СтрокаДоходы.ДоходВТекущемМесяцеШтат, 
																				СтрокаДоходы.ДоходВТекущемМесяцеАгент, 
																					СуммаРегрессаПФР); 
		
		СтрокаДоходы.НачисленноВзносовВПФРВТекущемМесяце_22 			= РасчитаныеВзносы.СуммаВзносовПФР22;
		СтрокаДоходы.НачисленноВзносовВПФРВТекущемМесяце_10 			= РасчитаныеВзносы.СуммаВзносовПФР10;
		СтрокаДоходы.НачисленноВзносовВПФРВТекущемМесяце_2210 			= РасчитаныеВзносы.ИтогоВзносовПФР1022;
		СтрокаДоходы.НачисленноВзносовВПФРВТекущемМесяце_22_Штат 		= РасчитаныеВзносы.СуммаВзносовПФР22_Ш;
		СтрокаДоходы.НачисленноВзносовВПФРВТекущемМесяце_10_Штат 		= РасчитаныеВзносы.СуммаВзносовПФР10_Ш;
		СтрокаДоходы.НачисленноВзносовВПФРВТекущемМесяце_2210_Штат 		= РасчитаныеВзносы.СуммаВзносовПФР1022_Ш;
		СтрокаДоходы.НачисленноВзносовВПФРВТекущемМесяце_22_Агент 		= РасчитаныеВзносы.СуммаВзносовПФР22_А;
		СтрокаДоходы.НачисленноВзносовВПФРВТекущемМесяце_10_Агент 		= РасчитаныеВзносы.СуммаВзносовПФР10_А;
		СтрокаДоходы.НачисленноВзносовВПФРВТекущемМесяце_2210_Агент 	= РасчитаныеВзносы.СуммаВзносовПФР1022_А;
		
	КонецЦикла; 
	
	Возврат Доходы;
	
КонецФункции

Функция РасчитатьВзносыПоШтатномуСотрудникуИАгенту(ДоходСНачалаГодаПоШтатномуСотруднику, ДоходСНачалаГодаПоАгенту, ДоходВТекущемМесяцеПоШтатномуСотруднику, ДоходВТекущемМесяцеПоАгенту,СуммаПорогПоПФР)
		
	ДоходСНачалаГодаПоШтатномуСотруднику 	= ?(ЗначениеЗаполнено(ДоходСНачалаГодаПоШтатномуСотруднику),ДоходСНачалаГодаПоШтатномуСотруднику,0);
	ДоходСНачалаГодаПоАгенту				= ?(ЗначениеЗаполнено(ДоходСНачалаГодаПоАгенту),ДоходСНачалаГодаПоАгенту,0);
	ДоходВТекущемМесяцеПоШтатномуСотруднику	= ?(ЗначениеЗаполнено(ДоходВТекущемМесяцеПоШтатномуСотруднику),ДоходВТекущемМесяцеПоШтатномуСотруднику,0);
	ДоходВТекущемМесяцеПоАгенту				= ?(ЗначениеЗаполнено(ДоходВТекущемМесяцеПоАгенту),ДоходВТекущемМесяцеПоАгенту,0);
	
	ДоходВТекущемМесяце = ДоходВТекущемМесяцеПоШтатномуСотруднику + ДоходВТекущемМесяцеПоАгенту;
	ДоходСНачалаГодаБезУчетаДоходаВТекущемМесяце = ДоходСНачалаГодаПоШтатномуСотруднику + ДоходСНачалаГодаПоАгенту;
	
	ДоходСНачалаГодаВключаяДоходВТекущемМесяце = ДоходВТекущемМесяце + ДоходСНачалаГодаБезУчетаДоходаВТекущемМесяце;
	
	Если ДоходВТекущемМесяцеПоШтатномуСотруднику <> 0 Тогда
		ДоляДоходаШтатникаВТекущемМесяце = ДоходВТекущемМесяцеПоШтатномуСотруднику / (ДоходВТекущемМесяце / 100);
	Иначе
		ДоляДоходаШтатникаВТекущемМесяце = 0;
	КонецЕсли;
	
	Если ДоходВТекущемМесяцеПоАгенту <> 0 Тогда
		ДоляДоходаАгентаВТекущемМесяце   = ДоходВТекущемМесяцеПоАгенту / (ДоходВТекущемМесяце /100);
	Иначе
		ДоляДоходаАгентаВТекущемМесяце = 0;
	КонецЕсли;
	
	Если ДоходСНачалаГодаВключаяДоходВТекущемМесяце <= СуммаПорогПоПФР Тогда
		
		// Нет регресса
		
		СуммаВзносовВПФРВТекущемМесяце10 = 0;
		СуммаВзносовВПФРВТекущемМесяце22 = (ДоходВТекущемМесяце / 100) * 22;
		СуммаВзносовВПФРВТекущемМесяце1022 = СуммаВзносовВПФРВТекущемМесяце22;
		
	Иначе	
		// Есть регресс
		
		Если ДоходСНачалаГодаБезУчетаДоходаВТекущемМесяце >= СуммаПорогПоПФР Тогда
			
			// Переход был в прошлых периодах
			
			СуммаВзносовВПФРВТекущемМесяце10 = (ДоходВТекущемМесяце / 100) * 10;
			СуммаВзносовВПФРВТекущемМесяце22 = 0;
			СуммаВзносовВПФРВТекущемМесяце1022 = СуммаВзносовВПФРВТекущемМесяце10;
			
		Иначе
			
			// Переход в текущем периоде    
					
			ДоходОблагаемыйВзносомПФРНарастающимИтогом10 = ДоходСНачалаГодаВключаяДоходВТекущемМесяце - СуммаПорогПоПФР;
			ДоходОблагаемыйВзносомПФРНарастающимИтогом22 = ДоходСНачалаГодаВключаяДоходВТекущемМесяце - ДоходОблагаемыйВзносомПФРНарастающимИтогом10;
			
			ДоходОблагаемыйВзносомПФРВТекущемМесяце22 = СуммаПорогПоПФР - ДоходСНачалаГодаБезУчетаДоходаВТекущемМесяце;	
			ДоходОблагаемыйВзносомПФРВТекущемМесяце10 = ДоходВТекущемМесяце - ДоходОблагаемыйВзносомПФРВТекущемМесяце22;
			
			СуммаВзносовВПФРВТекущемМесяце10 = (ДоходОблагаемыйВзносомПФРВТекущемМесяце10 / 100) * 10;
			СуммаВзносовВПФРВТекущемМесяце22 = (ДоходОблагаемыйВзносомПФРВТекущемМесяце22 / 100) * 22;
			СуммаВзносовВПФРВТекущемМесяце1022 = СуммаВзносовВПФРВТекущемМесяце10 + СуммаВзносовВПФРВТекущемМесяце22; 
			
		КонецЕсли;
			
	КонецЕсли;
		
	// Расчет долей между штатником и агентом
	
	Если ДоляДоходаШтатникаВТекущемМесяце <> 0 Тогда
		СуммаВзносовВПФРВТекущемМесяце10_ШтатныйСотрудник = СуммаВзносовВПФРВТекущемМесяце10 * ДоляДоходаШтатникаВТекущемМесяце/ 100;
		СуммаВзносовВПФРВТекущемМесяце22_ШтатныйСотрудник = СуммаВзносовВПФРВТекущемМесяце22 * ДоляДоходаШтатникаВТекущемМесяце/ 100;
		СуммаВзносовВПФРВТекущемМесяце1022_ШтатныйСотрудник = СуммаВзносовВПФРВТекущемМесяце10_ШтатныйСотрудник + СуммаВзносовВПФРВТекущемМесяце22_ШтатныйСотрудник;
	Иначе
		СуммаВзносовВПФРВТекущемМесяце10_ШтатныйСотрудник = 0;
		СуммаВзносовВПФРВТекущемМесяце22_ШтатныйСотрудник = 0;
		СуммаВзносовВПФРВТекущемМесяце1022_ШтатныйСотрудник = 0;
	КонецЕсли;
	
	Если ДоляДоходаАгентаВТекущемМесяце <> 0 Тогда	
		СуммаВзносовВПФРВТекущемМесяце10_Агент = СуммаВзносовВПФРВТекущемМесяце10  * ДоляДоходаАгентаВТекущемМесяце/ 100;
		СуммаВзносовВПФРВТекущемМесяце22_Агент = СуммаВзносовВПФРВТекущемМесяце22  * ДоляДоходаАгентаВТекущемМесяце/ 100;
		СуммаВзносовВПФРВТекущемМесяце1022_Агент = СуммаВзносовВПФРВТекущемМесяце10_Агент + СуммаВзносовВПФРВТекущемМесяце22_Агент;
	Иначе
		СуммаВзносовВПФРВТекущемМесяце10_Агент = 0;
		СуммаВзносовВПФРВТекущемМесяце22_Агент = 0;
		СуммаВзносовВПФРВТекущемМесяце1022_Агент = 0;
	КонецЕсли;
	
	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("ИтогоВзносовПФР1022", СуммаВзносовВПФРВТекущемМесяце1022);
	СтруктураРезультата.Вставить("СуммаВзносовПФР10", СуммаВзносовВПФРВТекущемМесяце10);
	СтруктураРезультата.Вставить("СуммаВзносовПФР22", СуммаВзносовВПФРВТекущемМесяце22);
	СтруктураРезультата.Вставить("ДоляШтатника", ДоляДоходаШтатникаВТекущемМесяце);
	СтруктураРезультата.Вставить("ДоляАгента", ДоляДоходаАгентаВТекущемМесяце);
	СтруктураРезультата.Вставить("СуммаВзносовПФР10_Ш", СуммаВзносовВПФРВТекущемМесяце10_ШтатныйСотрудник);
	СтруктураРезультата.Вставить("СуммаВзносовПФР22_Ш", СуммаВзносовВПФРВТекущемМесяце22_ШтатныйСотрудник);
	СтруктураРезультата.Вставить("СуммаВзносовПФР1022_Ш", СуммаВзносовВПФРВТекущемМесяце1022_ШтатныйСотрудник);
	СтруктураРезультата.Вставить("СуммаВзносовПФР10_А", СуммаВзносовВПФРВТекущемМесяце10_Агент);
	СтруктураРезультата.Вставить("СуммаВзносовПФР22_А", СуммаВзносовВПФРВТекущемМесяце22_Агент);
	СтруктураРезультата.Вставить("СуммаВзносовПФР1022_А", СуммаВзносовВПФРВТекущемМесяце1022_Агент);

	Возврат СтруктураРезультата;
	
КонецФункции

Функция ПолучитьТаблицуМесяцев(НачалоВыбранныйМесяц, КонецВыбранныйМесяц)
	
	МассивДат = Новый Массив;
	
    Запрос = Новый Запрос;
    Запрос.Текст = 
	"ВЫБРАТЬ
    |   НАЧАЛОПЕРИОДА(&НачДата, День) КАК Период
    |УПОРЯДОЧИТЬ ПО
    |   Период
    |ИТОГИ ПО
    |   Период ПЕРИОДАМИ(МЕСЯЦ, &НачДата, &КонДата)";
	
	Запрос.УстановитьПараметр("НачДата", НачалоМесяца(НачалоВыбранныйМесяц));
    Запрос.УстановитьПараметр("КонДата", КонецМесяца(КонецВыбранныйМесяц));

    Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Период", "ВСЕ");
	Пока Выборка.Следующий() Цикл
		МассивДат.Добавить(Выборка.Период);
    КонецЦикла;

    Возврат МассивДат;

КонецФункции 

Функция ПолучитьСуммуРегрессаПоПФР()
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Период", КонецДня(КонецПериода));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПредельнаяВеличинаБазыСтраховыхВзносовСрезПоследних.РазмерПФР
	|ИЗ
	|	РегистрСведений.ПредельнаяВеличинаБазыСтраховыхВзносов.СрезПоследних(&Период, ) КАК ПредельнаяВеличинаБазыСтраховыхВзносовСрезПоследних";
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		Если Выборка.Следующий() Тогда
			
			Возврат Выборка.РазмерПФР;
	
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат 0;
	
КонецФункции

Функция ПолучитьИтоговуюТаблицу()
	
	СуммуРегрессаПоПФР 	= ПолучитьСуммуРегрессаПоПФР();
	МассивМесяцев 		= ПолучитьТаблицуМесяцев(НачалоПериода, КонецПериода);
	COM_Соединение 		= ППФ_Сервер.УстановитьCOMПодключениеСБазойДанных1С();
	
	ДоходыЗаГод = Новый ТаблицаЗначений;
	ДоходыЗаГод.Колонки.Добавить("ФИО", Новый ОписаниеТипов("Строка"));
	ДоходыЗаГод.Колонки.Добавить("СНИЛС", Новый ОписаниеТипов("Строка"));
	ДоходыЗаГод.Колонки.Добавить("ДоходыШтатногоСотрудникаСНачалаГода", Новый ОписаниеТипов("Число"));
	ДоходыЗаГод.Колонки.Добавить("ДоходыАгентаСНачалаГода", Новый ОписаниеТипов("Число"));
	
	ДоходыЗаГод.Колонки.Добавить("НеОблагаемаяБазаСНачалаГодаШтат", Новый ОписаниеТипов("Число"));
	ДоходыЗаГод.Колонки.Добавить("НеОблагаемаяБазаСНачалаГодаАгент", Новый ОписаниеТипов("Число"));
	
	ДоходыЗаГод.Колонки.Добавить("ПФР_22_СНачалаГода", Новый ОписаниеТипов("Число"));
	ДоходыЗаГод.Колонки.Добавить("ПФР_10_СНачалаГода", Новый ОписаниеТипов("Число"));
	ДоходыЗаГод.Колонки.Добавить("ПФР_22_10_СНачалаГода", Новый ОписаниеТипов("Число"));
	
	ДоходыЗаМесяц = Новый ТаблицаЗначений;
	ДоходыЗаМесяц.Колонки.Добавить("ФИО", Новый ОписаниеТипов("Строка"));
	ДоходыЗаМесяц.Колонки.Добавить("СНИЛС", Новый ОписаниеТипов("Строка"));
	
	ДоходыЗаМесяц.Колонки.Добавить("ДоходШтатногоСотрудника_ВТекущемМесяце", Новый ОписаниеТипов("Число"));
	ДоходыЗаМесяц.Колонки.Добавить("ДоходАгента_ВТекущемМесяце", Новый ОписаниеТипов("Число"));
	
	ДоходыЗаМесяц.Колонки.Добавить("НеОблагаемаяБазаВТекущемМесяцеШтат", Новый ОписаниеТипов("Число"));
	ДоходыЗаМесяц.Колонки.Добавить("НеОблагаемаяБазаВТекущемМесяцеАгент", Новый ОписаниеТипов("Число"));
	
	ДоходыЗаМесяц.Колонки.Добавить("ПФР_22_ВТекущемМесяце", Новый ОписаниеТипов("Число"));
	ДоходыЗаМесяц.Колонки.Добавить("ПФР_10_ВТекущемМесяце", Новый ОписаниеТипов("Число"));
	ДоходыЗаМесяц.Колонки.Добавить("ПФР_22_10_ВТекущемМесяце", Новый ОписаниеТипов("Число"));
	
	ДоходыЗаМесяц.Колонки.Добавить("ПФР_ШтатногоСотрудника_22_ВТекущемМесяце", Новый ОписаниеТипов("Число"));
	ДоходыЗаМесяц.Колонки.Добавить("ПФР_ШтатногоСотрудника_10_ВТекущемМесяце", Новый ОписаниеТипов("Число"));
	ДоходыЗаМесяц.Колонки.Добавить("ПФР_ШтатногоСотрудника_22_10_ВТекущемМесяце", Новый ОписаниеТипов("Число"));

	ДоходыЗаМесяц.Колонки.Добавить("ПФР_Агента_22_ВТекущемМесяце", Новый ОписаниеТипов("Число"));
	ДоходыЗаМесяц.Колонки.Добавить("ПФР_Агента_10_ВТекущемМесяце", Новый ОписаниеТипов("Число"));
	ДоходыЗаМесяц.Колонки.Добавить("ПФР_Агента_22_10_ВТекущемМесяце", Новый ОписаниеТипов("Число"));
	
	КоличествоМесяцев = МассивМесяцев.Количество();
	ПоследнийМесяц = МассивМесяцев[КоличествоМесяцев - 1];
	
	Для Каждого ВыбранныйМесяц Из МассивМесяцев Цикл
		
		Доходы = ПолучитьДоходы(НачалоПериода,ВыбранныйМесяц, СуммуРегрессаПоПФР, COM_Соединение);
		
		Для Каждого СтрокаДоходы Из Доходы Цикл
						
			НоваяСтрокаДоходыЗаГод 							= ДоходыЗаГод.Добавить();
			НоваяСтрокаДоходыЗаГод.ФИО 						= СтрокаДоходы.ФИО;
			НоваяСтрокаДоходыЗаГод.СНИЛС 					= СтрокаДоходы.СНИЛС;
			
			НоваяСтрокаДоходыЗаГод.ПФР_22_СНачалаГода 		= НоваяСтрокаДоходыЗаГод.ПФР_22_СНачалаГода 	+ СтрокаДоходы.НачисленноВзносовВПФРВТекущемМесяце_22;
			НоваяСтрокаДоходыЗаГод.ПФР_10_СНачалаГода		= НоваяСтрокаДоходыЗаГод.ПФР_10_СНачалаГода 	+ СтрокаДоходы.НачисленноВзносовВПФРВТекущемМесяце_10;
			НоваяСтрокаДоходыЗаГод.ПФР_22_10_СНачалаГода	= НоваяСтрокаДоходыЗаГод.ПФР_22_10_СНачалаГода 	+ СтрокаДоходы.НачисленноВзносовВПФРВТекущемМесяце_2210;
			
			Если ПоследнийМесяц = ВыбранныйМесяц Тогда
				
				НоваяСтрокаДоходыЗаГод.ДоходыШтатногоСотрудникаСНачалаГода 				= СтрокаДоходы.ДоходСНачалаГодаШтат + СтрокаДоходы.ДоходВТекущемМесяцеШтат;
				НоваяСтрокаДоходыЗаГод.ДоходыАгентаСНачалаГода 							= СтрокаДоходы.ДоходСНачалаГодаАгент + СтрокаДоходы.ДоходВТекущемМесяцеАгент;
				НоваяСтрокаДоходыЗаГод.НеОблагаемаяБазаСНачалаГодаШтат					= СтрокаДоходы.НеОблагаемаяБазаСНачалаГодаШтат;
				НоваяСтрокаДоходыЗаГод.НеОблагаемаяБазаСНачалаГодаАгент					= СтрокаДоходы.НеОблагаемаяБазаСНачалаГодаАгент;
				
				НоваяСтрокаДоходыЗаМесяц 												= ДоходыЗаМесяц.Добавить();
				НоваяСтрокаДоходыЗаМесяц.ФИО 											= СтрокаДоходы.ФИО;
				НоваяСтрокаДоходыЗаМесяц.СНИЛС 											= СтрокаДоходы.СНИЛС;
				
				НоваяСтрокаДоходыЗаМесяц.ДоходШтатногоСотрудника_ВТекущемМесяце 		= СтрокаДоходы.ДоходВТекущемМесяцеШтат;
				НоваяСтрокаДоходыЗаМесяц.ДоходАгента_ВТекущемМесяце						= СтрокаДоходы.ДоходВТекущемМесяцеАгент;
				
				НоваяСтрокаДоходыЗаМесяц.НеОблагаемаяБазаВТекущемМесяцеШтат				= СтрокаДоходы.НеОблагаемаяБазаВТекущемМесяцеШтат;
				НоваяСтрокаДоходыЗаМесяц.НеОблагаемаяБазаВТекущемМесяцеАгент			= СтрокаДоходы.НеОблагаемаяБазаВТекущемМесяцеАгент;
				
				НоваяСтрокаДоходыЗаМесяц.ПФР_22_ВТекущемМесяце							= СтрокаДоходы.НачисленноВзносовВПФРВТекущемМесяце_22;
				НоваяСтрокаДоходыЗаМесяц.ПФР_10_ВТекущемМесяце							= СтрокаДоходы.НачисленноВзносовВПФРВТекущемМесяце_10;
				НоваяСтрокаДоходыЗаМесяц.ПФР_22_10_ВТекущемМесяце						= СтрокаДоходы.НачисленноВзносовВПФРВТекущемМесяце_2210;
				
				НоваяСтрокаДоходыЗаМесяц.ПФР_ШтатногоСотрудника_22_ВТекущемМесяце 		= СтрокаДоходы.НачисленноВзносовВПФРВТекущемМесяце_22_Штат;
				НоваяСтрокаДоходыЗаМесяц.ПФР_ШтатногоСотрудника_10_ВТекущемМесяце 		= СтрокаДоходы.НачисленноВзносовВПФРВТекущемМесяце_10_Штат;
				НоваяСтрокаДоходыЗаМесяц.ПФР_ШтатногоСотрудника_22_10_ВТекущемМесяце 	= СтрокаДоходы.НачисленноВзносовВПФРВТекущемМесяце_2210_Штат;
				
				НоваяСтрокаДоходыЗаМесяц.ПФР_Агента_22_ВТекущемМесяце 					= СтрокаДоходы.НачисленноВзносовВПФРВТекущемМесяце_22_Агент;
				НоваяСтрокаДоходыЗаМесяц.ПФР_Агента_10_ВТекущемМесяце 					= СтрокаДоходы.НачисленноВзносовВПФРВТекущемМесяце_10_Агент;
				НоваяСтрокаДоходыЗаМесяц.ПФР_Агента_22_10_ВТекущемМесяце 				= СтрокаДоходы.НачисленноВзносовВПФРВТекущемМесяце_2210_Агент; 
							
			КонецЕсли;
			    	
		КонецЦикла;  
			
	КонецЦикла;  
	
	ИтоговаяТаблица = Новый ТаблицаЗначений;
	ИтоговаяТаблица.Колонки.Добавить("ФИО", Новый ОписаниеТипов("Строка"));
	ИтоговаяТаблица.Колонки.Добавить("СНИЛС", Новый ОписаниеТипов("Строка"));
	ИтоговаяТаблица.Колонки.Добавить("ДоходыШтатногоСотрудникаСНачалаГода", Новый ОписаниеТипов("Число"));
	ИтоговаяТаблица.Колонки.Добавить("ДоходыАгентаСНачалаГода", Новый ОписаниеТипов("Число"));
	
	ИтоговаяТаблица.Колонки.Добавить("ПФР_22_СНачалаГода", Новый ОписаниеТипов("Число"));
	ИтоговаяТаблица.Колонки.Добавить("ПФР_10_СНачалаГода", Новый ОписаниеТипов("Число"));
	ИтоговаяТаблица.Колонки.Добавить("ПФР_22_10_СНачалаГода", Новый ОписаниеТипов("Число"));
	
	ИтоговаяТаблица.Колонки.Добавить("ДоходШтатногоСотрудника_ВТекущемМесяце", Новый ОписаниеТипов("Число"));
	ИтоговаяТаблица.Колонки.Добавить("ДоходАгента_ВТекущемМесяце", Новый ОписаниеТипов("Число"));
	
	ИтоговаяТаблица.Колонки.Добавить("ПФР_22_ВТекущемМесяце", Новый ОписаниеТипов("Число"));
	ИтоговаяТаблица.Колонки.Добавить("ПФР_10_ВТекущемМесяце", Новый ОписаниеТипов("Число"));
	ИтоговаяТаблица.Колонки.Добавить("ПФР_22_10_ВТекущемМесяце", Новый ОписаниеТипов("Число"));
	
	ИтоговаяТаблица.Колонки.Добавить("ПФР_ШтатногоСотрудника_22_ВТекущемМесяце", Новый ОписаниеТипов("Число"));
	ИтоговаяТаблица.Колонки.Добавить("ПФР_ШтатногоСотрудника_10_ВТекущемМесяце", Новый ОписаниеТипов("Число"));
	ИтоговаяТаблица.Колонки.Добавить("ПФР_ШтатногоСотрудника_22_10_ВТекущемМесяце", Новый ОписаниеТипов("Число"));

	ИтоговаяТаблица.Колонки.Добавить("ПФР_Агента_22_ВТекущемМесяце", Новый ОписаниеТипов("Число"));
	ИтоговаяТаблица.Колонки.Добавить("ПФР_Агента_10_ВТекущемМесяце", Новый ОписаниеТипов("Число"));
	ИтоговаяТаблица.Колонки.Добавить("ПФР_Агента_22_10_ВТекущемМесяце", Новый ОписаниеТипов("Число"));
	
	ИтоговаяТаблица.Колонки.Добавить("НеОблагаемаяБазаСНачалаГодаШтат", Новый ОписаниеТипов("Число"));
	ИтоговаяТаблица.Колонки.Добавить("НеОблагаемаяБазаВТекущемМесяцеШтат", Новый ОписаниеТипов("Число"));
	ИтоговаяТаблица.Колонки.Добавить("НеОблагаемаяБазаВТекущемМесяцеАгент", Новый ОписаниеТипов("Число"));
	ИтоговаяТаблица.Колонки.Добавить("НеОблагаемаяБазаСНачалаГодаАгент", Новый ОписаниеТипов("Число"));
	
	Для Каждого СтрокаДоходыЗаГод1 Из ДоходыЗаГод Цикл
		НоваяСтрокаИтоговаяТаблица1 = ИтоговаяТаблица.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаИтоговаяТаблица1, СтрокаДоходыЗаГод1);
	КонецЦикла;  
	
	Для Каждого СтрокаДоходыЗаМесяц1 Из ДоходыЗаМесяц Цикл
		НоваяСтрокаИтоговаяТаблица2 = ИтоговаяТаблица.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаИтоговаяТаблица2, СтрокаДоходыЗаМесяц1);
	КонецЦикла;
	
	ИтоговаяТаблица.Свернуть("ФИО,СНИЛС", "НеОблагаемаяБазаВТекущемМесяцеАгент,НеОблагаемаяБазаСНачалаГодаАгент,НеОблагаемаяБазаСНачалаГодаШтат,НеОблагаемаяБазаВТекущемМесяцеШтат,ДоходыШтатногоСотрудникаСНачалаГода,ДоходыАгентаСНачалаГода,ПФР_22_СНачалаГода,ПФР_10_СНачалаГода,ПФР_22_10_СНачалаГода,ДоходШтатногоСотрудника_ВТекущемМесяце,ДоходАгента_ВТекущемМесяце,ПФР_22_ВТекущемМесяце,ПФР_10_ВТекущемМесяце,ПФР_22_10_ВТекущемМесяце,ПФР_ШтатногоСотрудника_22_ВТекущемМесяце,ПФР_ШтатногоСотрудника_10_ВТекущемМесяце,ПФР_ШтатногоСотрудника_22_10_ВТекущемМесяце,ПФР_Агента_22_ВТекущемМесяце,ПФР_Агента_10_ВТекущемМесяце,ПФР_Агента_22_10_ВТекущемМесяце");	
	ИтоговаяТаблица.Сортировать("ФИО,СНИЛС");
	
	Возврат ИтоговаяТаблица;
	
КонецФункции

#КонецОбласти 