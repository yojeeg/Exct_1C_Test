Функция ПолучитьТаблицуКоманд()
	
	Команды = Новый ТаблицаЗначений;
	// Как будет выглядеть описание печатной формы для пользователя
	Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	// Имя нашего макета, что бы могли отличить вызванную команду в обработке печати
	Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	// Тут задается, как должна вызваться команда обработки
	// Возможные варианты:
	// - ОткрытиеФормы - в этом случае в колонке идентификатор должно быть указано имя формы, которое должна будет открыть система
	// - ВызовКлиентскогоМетода - вызвать клиентскую экспортную процедуру из модуля формы обработки
	// - ВызовСерверногоМетода - вызвать серверную экспортную процедуру из модуля объекта обработки
	Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));
	// Следующий параметр указывает, необходимо ли показывать оповещение при начале и завершению работы обработки. Не имеет смысла при открытии формы
	Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));
	// Для печатной формы должен содержать строку ПечатьMXL 
	Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
	
	Возврат Команды;
	
КонецФункции

//Создает в таблице команд новую строку

Функция ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
	НоваяКоманда = ТаблицаКоманд.Добавить();
	НоваяКоманда. Представление = Представление;
	НоваяКоманда. Идентификатор= Идентификатор;
	НоваяКоманда. Использование= Использование;
	НоваяКоманда. ПоказыватьОповещение= ПоказыватьОповещение;
	НоваяКоманда. Модификатор= Модификатор;
КонецФункции

Функция СведенияОВнешнейОбработке() Экспорт
	ПараметрыРегистрации = Новый Структура;
	МассивНазначений = Новый Массив;
	МассивНазначений.Добавить("Документ.ПриемНаРаботу");
	ПараметрыРегистрации.Вставить("Вид", "ПечатнаяФорма"); //может быть - ЗаполнениеОбъекта, ДополнительныйОтчет, СозданиеСвязанныхОбъектов... 
	ПараметрыРегистрации.Вставить("Назначение", МассивНазначений);
	ПараметрыРегистрации.Вставить("Наименование", " Прием на работу-Т1б "); //имя под которым обработка будет зарегестрирована в справочнике внешних обработок
	ПараметрыРегистрации.Вставить("Версия", "1.0");
	ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);
	ПараметрыРегистрации.Вставить("Информация", " Прием на работу (Т1б) ");//так будет выглядеть описание печ.формы для пользователя
	ТаблицаКоманд = ПолучитьТаблицуКоманд();
	ДобавитьКоманду(ТаблицаКоманд, "ППФ Приказ о приеме (Т-1б)", "ПФ_MXL_Т1б", "ВызовСерверногоМетода", Истина);
	ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
	Возврат ПараметрыРегистрации;
КонецФункции

Процедура Печать(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Макет = ПолучитьМакет("ПФ_MXL_Т1б");
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_Т1б") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
		"ПФ_MXL_Т1б", "Приказ о приеме Т1б",	ПолучитьТабличныйДокументПриказаТ1(Макет,МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
КонецПроцедуры

// Процедура печати документа.
// Возвращает табличный документ - сформированную печатную форму приказа о приеме или увольнении 
//
// Параметры:
//	МассивОбъектов - массив сотрудников
//  ОбъектыПечати  - Список значений  - Объекты печати (значение - ссылка на объект, представление - имя области в которой был выведен объект)
//	ВидПриказа     - "ПриказОПриеме" или "ПриказОбУвольнении"
//
// Возвращаемое значение:
//	Табличный документ
//
Функция ПолучитьТабличныйДокументПриказаТ1(Макет, МассивОбъектов, ОбъектыПечати)
	
	ДокументРезультат = Новый ТабличныйДокумент;
	НомерСтрокиНачало = ДокументРезультат.ВысотаТаблицы + 1;
	
	ДокументРезультат.КлючПараметровПечати = "ПараметрыПечати_ПриказОПриеме";
	МассивДанныхЗаполнения = ПолучитьДанныеДляПечатиКадровогоПриказаТ1(МассивОбъектов);	
	
	ВывестиДанныеКадровогоПриказаВТабличныйДокумент(Макет, ДокументРезультат, МассивДанныхЗаполнения, ОбъектыПечати);
	
	ДокументРезультат.АвтоМасштаб = Истина;
	
	Возврат ДокументРезультат;
КонецФункции 

Функция ПолучитьДанныеДляПечатиКадровогоПриказаТ1(МассивОбъектов)
	
	Выборка = КадровыйУчет.СформироватьЗапросДляТ1(МассивОбъектов).Выбрать();
	
	МассивПараметров = Новый Массив;
	
	Пока  Выборка.Следующий() Цикл
		
		Параметры = ПолучитьСтруктуруПараметровПриказаТ1();		
		
		КадровыйУчет.ЗаполнитьПараметрыКадровогоПриказа(Параметры, Выборка);
		
		Параметры.НомерДок = Выборка.НомерДок;
		
		РезультатСклонения = "";
		Если ФизическиеЛицаЗарплатаКадры.Просклонять(Параметры.Работник, 4, РезультатСклонения, Параметры.Пол) Тогда
			Параметры.Работник	= РезультатСклонения
		КонецЕсли;
		
		ЦелаяЧасть = Цел(Выборка.ТарифнаяСтавка);
		Параметры.ОкладТарифнаяСтавкаЦелаяЧасть = ЦелаяЧасть;
		ДробнаяЧасть = Выборка.ТарифнаяСтавка - ЦелаяЧасть;
		Параметры.ОкладТарифнаяСтавкаДробнаяЧасть = ?(ДробнаяЧасть = 0, "00", ДробнаяЧасть*100);
		
		Если ЗначениеЗаполнено(Выборка.Надбавка1) Тогда 
			ЦелаяЧастьНадбавки = Цел(Выборка.Надбавка1);
			Параметры.Надбавка = ЦелаяЧастьНадбавки;
			ДробнаяЧастьНадбавки = Выборка.Надбавка1 - ЦелаяЧастьНадбавки;
			Параметры.Вставить("НадбавкаДробнаяЧасть",?(ДробнаяЧастьНадбавки = 0, "00", ДробнаяЧастьНадбавки*100));
		КонецЕсли;
		
		Параметры.Вставить("ВалютаНадбавки",Параметры.ВалютаТарифнойСтавки);
		Параметры.Вставить("КопейкиНадбавки",Параметры.КопейкиТарифнойСтавки);
		
		// Подразделение
		Если Параметры.Свойство("Подразделение") Тогда 
			Подразделение = Параметры.Подразделение;
			
			ЗапросПоПодразделениям = Новый Запрос;
			ТекстЗапроса = "ВЫБРАТЬ
			|	ДополнительныеСведения.Объект КАК Объект,
			|	ДополнительныеСведения.Свойство КАК Свойство,
			|	ДополнительныеСведения.Значение КАК Значение,
			|	""1"" КАК УровеньВложенности
			|ИЗ
			|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
			|ГДЕ
			|	ДополнительныеСведения.Объект = &Ссылка
			|ОБЪЕДИНИТЬ ВСЕ                             	
			|ВЫБРАТЬ
			|	ДополнительныеСведения.Объект,
			|	ДополнительныеСведения.Свойство,
			|	ДополнительныеСведения.Значение,
			|	""2""
			|ИЗ
			|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
			|ГДЕ
			|	ДополнительныеСведения.Объект = &СсылкаРодитель
			
			|ОБЪЕДИНИТЬ ВСЕ
			
			|ВЫБРАТЬ
			|	ДополнительныеСведения.Объект,
			|	ДополнительныеСведения.Свойство,
			|	ДополнительныеСведения.Значение,
			|	""3""
			|ИЗ
			|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
			|ГДЕ
			|	ДополнительныеСведения.Объект = &СсылкаРодительРодитель";
			ЗапросПоПодразделениям.Текст = ТекстЗапроса;
			ЗапросПоПодразделениям.УстановитьПараметр("Ссылка",Подразделение);
			ЗапросПоПодразделениям.УстановитьПараметр("СсылкаРодитель",Подразделение.Родитель);
			ЗапросПоПодразделениям.УстановитьПараметр("СсылкаРодительРодитель",Подразделение.Родитель.Родитель);
			
			ВыгрузкаПодразделений = ЗапросПоПодразделениям.Выполнить().Выгрузить();
			
			СтрокаПодразделения = "";
			МассивУровней = Новый Массив;
			МассивУровней.Добавить("1");
			МассивУровней.Добавить("2");
			МассивУровней.Добавить("3");
			Для Каждого УровеньВложенности из МассивУровней Цикл 
				Отбор = Новый Структура("УровеньВложенности",УровеньВложенности);
				ТаблицаСОтбором = ВыгрузкаПодразделений.Скопировать(Отбор);
				Если УровеньВложенности = "1" И ТаблицаСОтбором.Количество()=0 Тогда 
					СтрокаПодразделения = Подразделение;
					Прервать;
				КонецЕсли;
				
				Если УровеньВложенности = "1" Тогда 
					Для Каждого Строка из ТаблицаСОтбором Цикл 
						Если Строка.Свойство.Заголовок = "Винительный падеж" Тогда 
							СтрокаПодразделения = СтрокаПодразделения + " " + Строка.Значение;
						КонецЕсли;
					КонецЦикла;
				Иначе 
					Для Каждого Строка из ТаблицаСОтбором Цикл 
						Если Строка.Свойство.Заголовок = "Родительный падеж" Тогда 
							СтрокаПодразделения = СтрокаПодразделения + " " + Строка.Значение;
						КонецЕсли;
					КонецЦикла;	
				КонецЕсли;
			КонецЦикла;
			
			Параметры.Подразделение = СокрЛП(СтрокаПодразделения);
			
		КонецЕсли;
		
		РайКоэ = ПолучитьРайКоэф(Выборка.Сотрудник, КонецДня(Параметры.ДатаПриема));
		СерКоэ = ПолучитьСевернуюНадбавку(Выборка.Сотрудник, КонецДня(Параметры.ДатаПриема));
		Если СерКоэ = 0 Тогда 
			СерКоэ = Подразделение.ПроцентСевернойНадбавки;
		КонецЕсли;
		Если РайКоэ<>0 Тогда 
			РайКоэ = (РайКоэ-1)*100;
		КонецЕсли;
		
		Параметры.Вставить("РКцифрами",РайКоэ);
		Параметры.Вставить("ПроцЦифрами",СерКоэ);
		
		Сведения = "Руководитель,ДолжностьРуководителя";
		ОтветственныеЛица = СведенияОбОтветственныхЛицах.СведенияОбОтветственныхЛицах(Параметры.Ссылка.Организация, Сведения, Параметры.Ссылка.Дата);
		Параметры.ДолжностьРуководителя = ОтветственныеЛица.ДолжностьРуководителя;
		ЗапросФИОФизЛица = Новый Запрос();
		ЗапросФИОФизЛица.Текст = "ВЫБРАТЬ
		|	ФИОФизическихЛицСрезПоследних.Фамилия,
		|	ФИОФизическихЛицСрезПоследних.Имя,
		|	ФИОФизическихЛицСрезПоследних.Отчество
		|ИЗ
		|	РегистрСведений.ФИОФизическихЛиц.СрезПоследних(&Период, ФизическоеЛицо = &ФизическоеЛицо) КАК ФИОФизическихЛицСрезПоследних";
		ЗапросФИОФизЛица.УстановитьПараметр("Период",Параметры.Ссылка.Дата);
		ЗапросФИОФизЛица.УстановитьПараметр("ФизическоеЛицо",ОтветственныеЛица.Руководитель);
		
		ВыборкаФИОФизЛица = ЗапросФИОФизЛица.Выполнить().Выбрать();
		
		Если ВыборкаФИОФизЛица.Следующий() Тогда 
			Параметры.РуководительРасшифровкаПодписи = Лев(ВыборкаФИОФизЛица.Имя,1) +"." + Лев(ВыборкаФИОФизЛица.Отчество,1) +". " + ВыборкаФИОФизЛица.Фамилия;
		КонецЕсли;
		
		Если Параметры.Свойство("УсловияПриема") Тогда 
			Если Параметры.УсловияПриема = "Основное место работы, полная занятость" Тогда 
				Параметры.УсловияПриема = "Основное место работы, постоянно";
			КонецЕсли;
		КонецЕсли;

		
		МассивПараметров.Добавить(Параметры);
		
	КонецЦикла;
	
	Возврат МассивПараметров;

КонецФункции	

Функция ПолучитьСевернуюНадбавку(Сотрудник, Дата)
	
	Значение = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст  = "ВЫБРАТЬ
	                |	ЗначенияПериодическихПоказателейРасчетаЗарплатыСотрудниковСрезПоследних.Значение
	                |ИЗ
	                |	РегистрСведений.ЗначенияПериодическихПоказателейРасчетаЗарплатыСотрудников.СрезПоследних(
	                |			&ДатаДокумента,
	                |			Сотрудник = &Сотрудник
	                |				И Показатель = &Показатель) КАК ЗначенияПериодическихПоказателейРасчетаЗарплатыСотрудниковСрезПоследних";
	Запрос.УстановитьПараметр("Сотрудник",Сотрудник);					
	Запрос.УстановитьПараметр("ДатаДокумента",Дата);
	Запрос.УстановитьПараметр("Показатель",Справочники.ПоказателиРасчетаЗарплаты.СевернаяНадбавка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда 
		Значение = Выборка.Значение;
	КонецЕсли;
	
	Возврат Значение;	
	
КонецФункции

Функция ПолучитьРайКоэф(Сотрудник, Дата)
	
	Значение = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст  = "ВЫБРАТЬ
	                |	ЗначенияПериодическихПоказателейРасчетаЗарплатыСотрудниковСрезПоследних.Значение
	                |ИЗ
	                |	РегистрСведений.ЗначенияПериодическихПоказателейРасчетаЗарплатыСотрудников.СрезПоследних(
	                |			&ДатаДокумента,
	                |			Сотрудник = &Сотрудник
	                |				И Показатель = &Показатель) КАК ЗначенияПериодическихПоказателейРасчетаЗарплатыСотрудниковСрезПоследних";
	Запрос.УстановитьПараметр("Сотрудник",Сотрудник);					
	Запрос.УстановитьПараметр("ДатаДокумента",Дата);
	Запрос.УстановитьПараметр("Показатель",Справочники.ПоказателиРасчетаЗарплаты.РайонныйКоэффициент);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда 
		Значение = Выборка.Значение;
	КонецЕсли;
	
	Возврат Значение;	
	
КонецФункции



Процедура ВывестиДанныеКадровогоПриказаВТабличныйДокумент(Макет, ТабличныйДокумент, МассивДанныхЗаполнения, ОбъектыПечати)
	
	Если Макет <> Неопределено Тогда
		
		ПервыйПриказ = Истина;
		Для каждого ПараметрыМакета из МассивДанныхЗаполнения Цикл
			Если Не ПервыйПриказ Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			Иначе
				ПервыйПриказ = Ложь;
			КонецЕсли;
			
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
			
			Макет.Параметры.Заполнить(ПараметрыМакета);
			ТабличныйДокумент.Вывести(Макет);
			
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ПараметрыМакета.Ссылка);	
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры	

Функция ПолучитьСтруктуруПараметровПриказаТ1()
	
	Параметры = КадровыйУчет.ПараметрыКадровогоПриказа();
	
	Параметры.Вставить("ДатаПриема");
	Параметры.Вставить("НомерДок");
	Параметры.Вставить("ДатаЗавершенияТрудовогоДоговора");
	Параметры.Вставить("УсловияПриема");
	Параметры.Вставить("ОкладТарифнаяСтавкаЦелаяЧасть");
	Параметры.Вставить("ОкладТарифнаяСтавкаДробнаяЧасть");
	Параметры.Вставить("ВалютаТарифнойСтавки", "руб.");
	Параметры.Вставить("КопейкиТарифнойСтавки", "коп.");
	Параметры.Вставить("Надбавка1", 0);
	Параметры.Вставить("ДлительностьИспытательногоСрока", "");

	Возврат Параметры;
	
КонецФункции	