//////////////////////////////
//ПРОГРАММНЫЙ ИНТЕРФЕЙС
//////////////////////////////
Функция СведенияОВнешнейОбработке() Экспорт
	
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке();
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиДополнительнаяОбработка();
	ПараметрыРегистрации.Наименование = ЭтотОбъект.Метаданные().Синоним;
	ПараметрыРегистрации.Версия = "1.0";
	ПараметрыРегистрации.Информация = ЭтотОбъект.Метаданные().Синоним;
	ПараметрыРегистрации.БезопасныйРежим = Ложь;
	
	Команда = ПараметрыРегистрации.Команды.Добавить();
	Команда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовСерверногоМетода();
	Команда.Представление = ЭтотОбъект.Метаданные().Синоним;
	Команда.Идентификатор = ЭтотОбъект.Метаданные().Имя;
	
	Возврат ПараметрыРегистрации;	
	
КонецФункции
//////////////////////////////


///////////////////////////////
//ОСНОВНЫЕ КОМАНДЫ
///////////////////////////////
Процедура ВыполнитьКоманду(ИдентификаторКоманды = Неопределено) Экспорт
	
	Соединение = ППФ_Сервер.MSSQL_ПолучитьСоединение("DocsVision");	
	
	// Получим все записи, у которых номер паспорта не заполнен
	СотрудникиДляЗаполнения = ППФ_Сервер.MSSQL_ПолучитьРезультатЗапроса(ПолучитьТекстЗапросаНезаполненныеСотрудники(), Соединение);
	Если СотрудникиДляЗаполнения <> Неопределено И ТипЗнч(СотрудникиДляЗаполнения) = Тип("ТаблицаЗначений") Тогда 
		
		МассивСотрудников = СотрудникиДляЗаполнения.ВыгрузитьКолонку("TabNum");		
		Если МассивСотрудников.Количество()>0 Тогда 
			
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			               |	ДокументыФизическихЛицСрезПоследних.Серия КАК Серия,
			               |	ДокументыФизическихЛицСрезПоследних.Номер КАК Номер,
			               |	ДокументыФизическихЛицСрезПоследних.ДатаВыдачи КАК ДатаВыдачи,
			               |	ДокументыФизическихЛицСрезПоследних.КемВыдан КАК КемВыдан,
			               |	ДокументыФизическихЛицСрезПоследних.Физлицо.ДатаРождения КАК ДатаРождения,
			               |	Сотрудники.Код КАК Код,
			               |	Сотрудники.Ссылка КАК Ссылка
			               |ИЗ
			               |	Справочник.Сотрудники КАК Сотрудники
			               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДокументыФизическихЛиц.СрезПоследних КАК ДокументыФизическихЛицСрезПоследних
			               |		ПО Сотрудники.ФизическоеЛицо = ДокументыФизическихЛицСрезПоследних.Физлицо
			               |ГДЕ
			               |	ДокументыФизическихЛицСрезПоследних.ВидДокумента В(&ВидыДокумента)
			               |	И Сотрудники.Код В(&МассивТабНомеров)
			               |	И НЕ Сотрудники.ППФ_Агент";
			ВидыДокумента = Новый Массив;
			ВидыДокумента.Добавить(Справочники.ВидыДокументовФизическихЛиц.ПаспортРФ);
			ВидыДокумента.Добавить(Справочники.ВидыДокументовФизическихЛиц.НайтиПоНаименованию("Иностранный паспорт"));
			
			Запрос.УстановитьПараметр("ВидыДокумента",ВидыДокумента);
			Запрос.УстановитьПараметр("МассивТабНомеров",МассивСотрудников);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл 
				Если Не ППФ_Сервер.MSSQL_ВыполнитьЗапрос(ПолучитьТекстЗапросаОбновитьДанныеСотрудника(СокрЛП(Выборка.Код),Выборка.ДатаРождения, СокрЛП(СтрЗаменить(Выборка.Серия," ",""))+СокрЛП(Выборка.Номер), Выборка.ДатаВыдачи, СокрЛП(Выборка.КемВыдан) ),Соединение) Тогда 			 
					ППФ_Сервер.MSSQL_ЗаписатьВЖурналРегистрации("Произошла ошибка синхронизации с Docs Vision. Ошибка возникла при синхронизации сотрудника: "  + Выборка.Ссылка);
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;

	
КонецПроцедуры

///////////////////////////////
//СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
///////////////////////////////
Функция ПолучитьТекстЗапросаНезаполненныеСотрудники()
	Возврат "SELECT [TabNum]
      |,[CardId]
      |,[Birthday]
      |,[PassNum]
      |,[PassDate]
      |,[KemVydan]
  	  | FROM [ZYP-DV]
  	  |WHERE [PassNum] is Null";
КонецФункции

Функция ПолучитьТекстЗапросаОбновитьДанныеСотрудника(TabNum, Birthday,PassNum,PassDate,KemVydan)
	Возврат "UPDATE 
	|	dbo.[ZYP-DV] SET
	|	Birthday = '"  + Формат(Birthday,"ДФ=дд/ММ/гггг") + "',
	|	PassNum	 ='" + СокрЛП(PassNum) + "',
	|	PassDate = '" + Формат(PassDate,"ДФ=дд/ММ/гггг")  + "', 	
	|	KemVydan	 ='" + СокрЛП(KemVydan) + "'	
	|	WHERE TabNum ='" + СокрЛП(TabNum) + "'";		
КонецФункции // ПолучитьТекстЗапросаВставитьАгенты()
