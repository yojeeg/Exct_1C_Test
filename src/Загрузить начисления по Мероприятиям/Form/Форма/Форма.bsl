
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Объект.Начисление = ПланыВидовРасчета.Начисления.НайтиПоНаименованию("Мероприятие");
	Год = Год(ТекущаяДата());
	НатуральныйДоход = Истина;
	РазовоеНачисление = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ПолучениеЗагруженнойТаблицыЗначений" Тогда
		
		ПолучениеДанныхИзЗагруженнойТаблицыЗначений(Параметр);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)
	
	СписокКолонок = Новый СписокЗначений;  	
	СписокКолонок.Добавить("Сотрудник");
	СписокКолонок.Добавить("Период");
	СписокКолонок.Добавить("Сумма");
		
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СписокКолонок", СписокКолонок);
	
	ОткрытьФорму("Обработка.ППФ_ОБЩ_ЗагрузкаИзТабличногоДокумента.Форма.ЗагрузкаИзТабличногоДокумента", ПараметрыФормы, ЭтаФорма);	

КонецПроцедуры

&НаСервере
Процедура ПолучениеДанныхИзЗагруженнойТаблицыЗначений(АдресВХранилищеЗначений)
	
	ТабЗнч = ПолучитьИзВременногоХранилища(АдресВХранилищеЗначений);
	Если ТипЗнч(ТабЗнч) = Тип("ТаблицаЗначений") Тогда
		
		Объект.Excel.Очистить();
			
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ТекущиеКадровыеДанныеСотрудников.Сотрудник,
		|	ТекущиеКадровыеДанныеСотрудников.Сотрудник.Наименование КАК Наименование
		|ИЗ
		|	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
		|ГДЕ
		|	ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения = ДАТАВРЕМЯ(1, 1, 1)
		|	ИЛИ ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения >= &ТекущаяДата";
		Запрос.УстановитьПараметр("ТекущаяДата",ТекущаяДата());
		ВыгрузкаСотрудников = Запрос.Выполнить().Выгрузить();

		Для Каждого Строка Из ТабЗнч Цикл
			
			ТабСтр=Объект.Excel.Добавить();
			СтрокиСотрудников = ВыгрузкаСотрудников.НайтиСтроки(Новый Структура("Наименование",СокрЛП(Строка.Сотрудник)));
			Если СтрокиСотрудников.Количество()>0 Тогда 
				ТабСтр["Сотрудник"] = СтрокиСотрудников[0].Сотрудник;
			КонецЕсли;			
			ДатыНачисления = ПолучитьДатуНачалаОкончания(Строка.Период);
			ТабСтр["ДатаНачала"] =  ДатыНачисления.ДатаНачала;
			ТабСтр["ДатаОкончания"] =  ДатыНачисления.ДатаОкончания;
			ТабСтр["Сумма"] =  Число(СтрЗаменить(Строка.Сумма," ",""));

		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПолучитьДатуНачалаОкончания(месяц)
	Если ПустаяСтрока(месяц) Тогда 
		СтруктураДат = Новый Структура("ДатаНачала, ДатаОкончания",Дата(1,1,1),Дата(1,1,1));
	Иначе		
		ТреубемыйМесяц = НРег(СокрЛП(месяц));
		МесяцыДаты = Новый Соответствие();
		МесяцыДаты.Вставить("январь",	Дата(Год,1,1));
		МесяцыДаты.Вставить("февраль",	Дата(Год,2,1));
		МесяцыДаты.Вставить("март",		Дата(Год,3,1));
		МесяцыДаты.Вставить("апрель",	Дата(Год,4,1));
		МесяцыДаты.Вставить("май",		Дата(Год,5,1));
		МесяцыДаты.Вставить("июнь",		Дата(Год,6,1));
		МесяцыДаты.Вставить("июль",		Дата(Год,7,1));
		МесяцыДаты.Вставить("август",	Дата(Год,8,1));
		МесяцыДаты.Вставить("сентябрь",	Дата(Год,9,1));
		МесяцыДаты.Вставить("октябрь",	Дата(Год,10,1));
		МесяцыДаты.Вставить("ноябрь",	Дата(Год,11,1));
		МесяцыДаты.Вставить("декабрь",	Дата(Год,12,1));
		
		СтруктураДат = Новый Структура("ДатаНачала, ДатаОкончания",НачалоМесяца(МесяцыДаты.Получить(ТреубемыйМесяц)),КонецМесяца(МесяцыДаты.Получить(ТреубемыйМесяц)));
	КонецЕсли;
	Возврат СтруктураДат;

КонецФункции

&НаКлиенте
Процедура СоздатьДокументы(Команда)
	ТекстСообщения = "";
	Если РазовоеНачисление Тогда 
		СоздатьДокументыНаСервере_РазовоеНачисление(ТекстСообщения);
	Иначе 
		СоздатьДокументыНаСервере_НатуральныйДоход(ТекстСообщения);
	КонецЕсли;
	Сообщить(ТекстСообщения);
КонецПроцедуры

&НаСервере
Процедура СоздатьДокументыНаСервере_РазовоеНачисление(ТекстСообщения)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТекущиеКадровыеДанныеСотрудников.Сотрудник,
	               |	ТекущиеКадровыеДанныеСотрудников.ТекущееПодразделение
	               |ИЗ
	               |	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников";
	ВыгрузкаПодразделений = Запрос.Выполнить().Выгрузить();				   
	
	Выгрузка = Объект.Excel.Выгрузить();
	НоваяТаблица = Выгрузка.Скопировать(,"ДатаНачала");
	НоваяТаблица.Свернуть("ДатаНачала");
	МассивДатНачала = НоваяТаблица.ВыгрузитьКолонку("ДатаНачала");
	
	Для Каждого ДатаНачисления Из МассивДатНачала Цикл 
		НовыйДокумент = Документы.РазовоеНачисление.СоздатьДокумент();
		НовыйДокумент.Дата				= ТекущаяДата();
		НовыйДокумент.МесяцНачисления	= НачалоМесяца(ДатаНачисления);
		НовыйДокумент.ДатаНачала		= НачалоМесяца(ДатаНачисления);
		НовыйДокумент.ДатаОкончания		= КонецМесяца(ДатаНачисления);
		НовыйДокумент.ПорядокВыплаты	= Перечисления.ХарактерВыплатыЗарплаты.Межрасчет;
		НовыйДокумент.ПланируемаяДатаВыплаты = ДатаНачисления;
		НовыйДокумент.Организация 		= Справочники.Организации.ОрганизацияПоУмолчанию();
		НовыйДокумент.Ответственный		= ПараметрыСеанса.ТекущийПользователь;
		НовыйДокумент.Начисление		= Объект.Начисление;
		НовыйДокумент.Комментарий		= "Мероприятие";
		ТЧ = НовыйДокумент.Начисления;
		
		КопияТаблицыДата = Выгрузка.Скопировать(Новый Структура("ДатаНачала", ДатаНачисления));
		Для Каждого Строка Из КопияТаблицыДата Цикл 
			СтрокиПодразделений = ВыгрузкаПодразделений.НайтиСтроки(Новый Структура("Сотрудник",Строка.Сотрудник));
			
			НоваяСтрока = ТЧ.Добавить();
			Если СтрокиПодразделений.Количество()>0 Тогда 
				НоваяСтрока.Подразделение = СтрокиПодразделений[0].ТекущееПодразделение;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.Результат 					= Строка.Сумма;
			НоваяСтрока.ФиксРасчет 					= Истина;
			НоваяСтрока.ПериодДействия 				= НачалоМесяца(ДатаНачисления);
		КонецЦикла;
		
		НовыйДокумент.Начислено			= ТЧ.Итог("Результат");
		
		Попытка
			НовыйДокумент.Записать();
			Если Не ПустаяСтрока(ТекстСообщения) Тогда 
				ТекстСообщения = ТекстСообщения + Символы.ВК;
			КонецЕсли;
			ТекстСообщения = ТекстСообщения + "Создан новый документ: " + НовыйДокумент.Ссылка;
		Исключение   
			Если Не ПустаяСтрока(ТекстСообщения) Тогда 
				ТекстСообщения = ТекстСообщения + Символы.ВК;
			КонецЕсли;
			ТекстСообщения = ТекстСообщения + "Не удалось записать документ: " + ОписаниеОшибки();
		КонецПопытки;
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура СоздатьДокументыНаСервере_НатуральныйДоход(ТекстСообщения)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТекущиеКадровыеДанныеСотрудников.Сотрудник,
	               |	ТекущиеКадровыеДанныеСотрудников.ТекущееПодразделение
	               |ИЗ
	               |	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников";
	ВыгрузкаПодразделений = Запрос.Выполнить().Выгрузить();				   
	
	Выгрузка = Объект.Excel.Выгрузить();
	НоваяТаблица = Выгрузка.Скопировать(,"ДатаНачала");
	НоваяТаблица.Свернуть("ДатаНачала");
	МассивДатНачала = НоваяТаблица.ВыгрузитьКолонку("ДатаНачала");
	
	Для Каждого ДатаНачисления Из МассивДатНачала Цикл 
		НовыйДокумент = Документы.ДоходВНатуральнойФорме.СоздатьДокумент();
		НовыйДокумент.Дата				= ТекущаяДата();
		НовыйДокумент.МесяцНачисления	= НачалоМесяца(ДатаНачисления);
		НовыйДокумент.Организация 		= Справочники.Организации.ОрганизацияПоУмолчанию();
		НовыйДокумент.Ответственный		= ПараметрыСеанса.ТекущийПользователь;
		НовыйДокумент.Начисление		= Объект.Начисление;
		НовыйДокумент.Комментарий		= "Мероприятие";
		ТЧ = НовыйДокумент.Начисления;
		
		КопияТаблицыДата = Выгрузка.Скопировать(Новый Структура("ДатаНачала", ДатаНачисления));
		Для Каждого Строка Из КопияТаблицыДата Цикл 
			СтрокиПодразделений = ВыгрузкаПодразделений.НайтиСтроки(Новый Структура("Сотрудник",Строка.Сотрудник));
			
			НоваяСтрока = ТЧ.Добавить();
			Если СтрокиПодразделений.Количество()>0 Тогда 
				НоваяСтрока.Подразделение = СтрокиПодразделений[0].ТекущееПодразделение;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.Результат 					= Строка.Сумма;
			НоваяСтрока.ФиксРасчет 					= Истина;
			НоваяСтрока.ПериодДействия 				= НачалоМесяца(ДатаНачисления);
		КонецЦикла;
		
		Попытка
			НовыйДокумент.Записать();
			Если Не ПустаяСтрока(ТекстСообщения) Тогда 
				ТекстСообщения = ТекстСообщения + Символы.ВК;
			КонецЕсли;
			ТекстСообщения = ТекстСообщения + "Создан новый документ: " + НовыйДокумент.Ссылка;
		Исключение   
			Если Не ПустаяСтрока(ТекстСообщения) Тогда 
				ТекстСообщения = ТекстСообщения + Символы.ВК;
			КонецЕсли;
			ТекстСообщения = ТекстСообщения + "Не удалось записать документ: " + ОписаниеОшибки();
		КонецПопытки;
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура РазовоеНачислениеПриИзмененииНаСервере()
	Если РазовоеНачисление Тогда 
		РазовоеНачисление = Истина;
		НатуральныйДоход = Ложь;
	Иначе 
		РазовоеНачисление = Ложь;
		НатуральныйДоход = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РазовоеНачислениеПриИзменении(Элемент)
	РазовоеНачислениеПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура НатуральныйДоходПриИзмененииНаСервере()
	Если НатуральныйДоход Тогда 
		РазовоеНачисление = Ложь;
		НатуральныйДоход = Истина;
	Иначе 
		РазовоеНачисление = Истина;
		НатуральныйДоход = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НатуральныйДоходПриИзменении(Элемент)
	НатуральныйДоходПриИзмененииНаСервере();
КонецПроцедуры


