
&НаСервере
Процедура ПолучитьДанныеНаСервере(ОтчетПоРазделу3)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	РегламентированныйОтчет.Ссылка КАК Ссылка,
	               |	РегламентированныйОтчет.ВерсияДанных КАК ВерсияДанных,
	               |	РегламентированныйОтчет.ПометкаУдаления КАК ПометкаУдаления,
	               |	РегламентированныйОтчет.Дата КАК Дата,
	               |	РегламентированныйОтчет.Проведен КАК Проведен,
	               |	РегламентированныйОтчет.Вид КАК Вид,
	               |	РегламентированныйОтчет.ВидОтчетности КАК ВидОтчетности,
	               |	РегламентированныйОтчет.ВыбраннаяФорма КАК ВыбраннаяФорма,
	               |	РегламентированныйОтчет.ДанныеОтчета КАК ДанныеОтчета,
	               |	РегламентированныйОтчет.ДатаНачала КАК ДатаНачала,
	               |	РегламентированныйОтчет.ДатаОкончания КАК ДатаОкончания,
	               |	РегламентированныйОтчет.ДатаПодписи КАК ДатаПодписи,
	               |	РегламентированныйОтчет.ДеревоНастройкиСтраниц КАК ДеревоНастройкиСтраниц,
	               |	РегламентированныйОтчет.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	РегламентированныйОтчет.ИсточникОтчета КАК ИсточникОтчета,
	               |	РегламентированныйОтчет.КодНалоговогоОргана КАК КодНалоговогоОргана,
	               |	РегламентированныйОтчет.КодНалоговогоОрганаПосредника КАК КодНалоговогоОрганаПосредника,
	               |	РегламентированныйОтчет.Комментарий КАК Комментарий,
	               |	РегламентированныйОтчет.НаименованиеОтчета КАК НаименованиеОтчета,
	               |	РегламентированныйОтчет.НомерПачки КАК НомерПачки,
	               |	РегламентированныйОтчет.Организация КАК Организация,
	               |	РегламентированныйОтчет.Период КАК Период,
	               |	РегламентированныйОтчет.Периодичность КАК Периодичность,
	               |	РегламентированныйОтчет.ПредставлениеВида КАК ПредставлениеВида,
	               |	РегламентированныйОтчет.ПредставлениеПериода КАК ПредставлениеПериода,
	               |	РегламентированныйОтчет.ТочностьЕдиницыИзмерения КАК ТочностьЕдиницыИзмерения,
	               |	РегламентированныйОтчет.ФорматВыгрузки КАК ФорматВыгрузки,
	               |	РегламентированныйОтчет.УдалитьКПП КАК УдалитьКПП,
	               |	РегламентированныйОтчет.КПП КАК КПП,
	               |	РегламентированныйОтчет.СтатусОтчета КАК СтатусОтчета,
	               |	РегламентированныйОтчет.Банк КАК Банк,
	               |	РегламентированныйОтчет.Идентификатор КАК Идентификатор,
	               |	РегламентированныйОтчет.Представление КАК Представление,
	               |	РегламентированныйОтчет.МоментВремени КАК МоментВремени
	               |ИЗ
	               |	Документ.РегламентированныйОтчет КАК РегламентированныйОтчет
	               |ГДЕ
	               |	РегламентированныйОтчет.НаименованиеОтчета = ""Расчет по страховым взносам""
	               |	И РегламентированныйОтчет.ПометкаУдаления = ЛОЖЬ
	               |	И РегламентированныйОтчет.ДатаНачала = &ДатаНачала";
	Запрос.УстановитьПараметр("ДатаНачала",ДатаНачала);   
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда 
		
		ТаблицаДанных = Новый ТаблицаЗначений;
		ТаблицаДанных.Колонки.Добавить("Фамилия");
		ТаблицаДанных.Колонки.Добавить("Имя");
		ТаблицаДанных.Колонки.Добавить("Отчество");
		ТаблицаДанных.Колонки.Добавить("СНИЛС");
		ТаблицаДанных.Колонки.Добавить("ИНН");
		ТаблицаДанных.Колонки.Добавить("Сумма210_1");
		ТаблицаДанных.Колонки.Добавить("Сумма210_2");
		ТаблицаДанных.Колонки.Добавить("Сумма210_3");
		ТаблицаДанных.Колонки.Добавить("Сумма220_1");
		ТаблицаДанных.Колонки.Добавить("Сумма220_2");
		ТаблицаДанных.Колонки.Добавить("Сумма220_3");
		ТаблицаДанных.Колонки.Добавить("Сумма240_1");
		ТаблицаДанных.Колонки.Добавить("Сумма240_2");
		ТаблицаДанных.Колонки.Добавить("Сумма240_3");
		
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл 
			ДанныеОтчета = Выборка.ДанныеОтчета;
			ДанныеИзХранилища = ДанныеОтчета.Получить();
			Если ТипЗнч(ДанныеИзХранилища) = Тип("Структура") 
				И ДанныеИзХранилища.Свойство("ДанныеМногоуровневыхРазделов")
				И ТипЗнч(ДанныеИзХранилища.ДанныеМногоуровневыхРазделов) = Тип("Структура") 
				И ДанныеИзХранилища.ДанныеМногоуровневыхРазделов.Свойство("Раздел3")
				И ТипЗнч(ДанныеИзХранилища.ДанныеМногоуровневыхРазделов.Раздел3) = Тип("ДеревоЗначений") 
				Тогда 
				
				ДеревоОтчета = ДанныеИзХранилища.ДанныеМногоуровневыхРазделов.Раздел3;
				СтрокиДереваОтчета = ДеревоОтчета.Строки;
				
				Для Каждого СтрокаДерева ИЗ СтрокиДереваОтчета Цикл 
					ОбщиеДанные = СтрокаДерева.Данные;
					Если ТипЗнч(ОбщиеДанные) = Тип("Структура")
						И ОбщиеДанные.Свойство("П000310006001") // ИНН
						И ОбщиеДанные.Свойство("П000310007001") // СНИЛС
						И ОбщиеДанные.Свойство("П000310008001") // Фамилия
						И ОбщиеДанные.Свойство("П000310009001") // Имя
						И ОбщиеДанные.Свойство("П000310010001") // Отчество
						Тогда 
						НоваяСтрока = ТаблицаДанных.Добавить();
						НоваяСтрока.Фамилия 	= ОбщиеДанные.П000310008001;
						НоваяСтрока.Имя 		= ОбщиеДанные.П000310009001;
						НоваяСтрока.Отчество 	= ОбщиеДанные.П000310010001;
						НоваяСтрока.СНИЛС 		= ОбщиеДанные.П000310007001;
						НоваяСтрока.ИНН 		= ОбщиеДанные.П000310006001;
						
						ПодробныеДанные = СтрокаДерева.ДанныеМногострочныхЧастей;
						Если ТипЗнч(ПодробныеДанные) = Тип("Структура")
							И ПодробныеДанные.Свойство("П00321М1")
							И ТипЗнч(ПодробныеДанные.П00321М1) = Тип("ДеревоЗначений")
							Тогда 
							
							СтрокиПодробныхДанных = ПодробныеДанные.П00321М1.Строки;

							Для Каждого СтрокаПодробныхДанных Из СтрокиПодробныхДанных Цикл 
								
								ДанныеПоСуммам = СтрокаПодробныхДанных.Данные;
								Если ТипЗнч(ДанныеПоСуммам) = Тип("Структура")
									И ДанныеПоСуммам.Свойство("П00321М121001")
									И ДанныеПоСуммам.Свойство("П00321М122001")
									И ДанныеПоСуммам.Свойство("П00321М124001")									
									Тогда 
									
									НоваяСтрока["Сумма210_"+Число(ДанныеПоСуммам.П00321М119001)] = ДанныеПоСуммам.П00321М121001;
									НоваяСтрока["Сумма220_"+Число(ДанныеПоСуммам.П00321М119001)] = ДанныеПоСуммам.П00321М122001;
									НоваяСтрока["Сумма240_"+Число(ДанныеПоСуммам.П00321М119001)] = ДанныеПоСуммам.П00321М124001;
																	
								КонецЕсли;
								
							КонецЦикла;
							
							
						КонецЕсли;
						
						

					КонецЕсли;
				КонецЦикла; 				
				
			КонецЕсли; 			
		КонецЦикла;
		
		
		Макет 			= РеквизитФормыВЗначение("Объект").ПолучитьМакет("Макет");
		Шапка 			= Макет.ПолучитьОбласть("Шапка");
		СтрокаОтчета 	= Макет.ПолучитьОбласть("СтрокаДанных");
		Подвал 			= Макет.ПолучитьОбласть("Подвал");
		ОтчетПоРазделу3.Вывести(Шапка);
		
		Если ТаблицаДанных.Количество()>0 Тогда 
			
			ТаблицаДанных.Сортировать("Фамилия, Имя, Отчество");
			
			счетчик = 1;
			Для Каждого СтрокаДанных Из ТаблицаДанных Цикл 
				ЗаполнитьЗначенияСвойств(СтрокаОтчета.Параметры, СтрокаДанных);
				СтрокаОтчета.Параметры.номерПП = счетчик;
				ОтчетПоРазделу3.Вывести(СтрокаОтчета);
				счетчик = счетчик + 1;
			КонецЦикла;    			
			
			Подвал.Параметры.ИтогоСумма210_1 = ТаблицаДанных.Итог("Сумма210_1");
			Подвал.Параметры.ИтогоСумма210_2 = ТаблицаДанных.Итог("Сумма210_2");
			Подвал.Параметры.ИтогоСумма210_3 = ТаблицаДанных.Итог("Сумма210_3");
			Подвал.Параметры.ИтогоСумма220_1 = ТаблицаДанных.Итог("Сумма220_1");
			Подвал.Параметры.ИтогоСумма220_2 = ТаблицаДанных.Итог("Сумма220_2");
			Подвал.Параметры.ИтогоСумма220_3 = ТаблицаДанных.Итог("Сумма220_3");
			Подвал.Параметры.ИтогоСумма240_1 = ТаблицаДанных.Итог("Сумма240_1");
			Подвал.Параметры.ИтогоСумма240_2 = ТаблицаДанных.Итог("Сумма240_2");
			Подвал.Параметры.ИтогоСумма240_3 = ТаблицаДанных.Итог("Сумма240_3");
			
			ОтчетПоРазделу3.Вывести(Подвал);			
			
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьДанные(Команда)
	
	ОтчетПоРазделу3 = Новый ТабличныйДокумент;	
	ПолучитьДанныеНаСервере(ОтчетПоРазделу3);
	ОтчетПоРазделу3.АвтоМасштаб = Истина;
	ОтчетПоРазделу3.ФиксацияСверху = 3;
	ОтчетПоРазделу3.Показать("РСВ. Суммы Раздела 3");
КонецПроцедуры


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ДатаНачала = НачалоКвартала(НачалоКвартала(ТекущаяДата())-1);
КонецПроцедуры
