&НаКлиенте
Перем СписокМакетов;

&НаСервере
Процедура РаспечататьНаСервере(НазваниеМакета,ТабличныйДокумент)	
	РеквизитОбъект = РеквизитФормыВЗначение("Объект");
	МакетПечати = РеквизитОбъект.ПолучитьМакет(НазваниеМакета);
	ПолеДокумент = МакетПечати.ПолучитьОбласть("Документ");
	Для Каждого Строка Из СписокСотрудников Цикл 
		Сотрудник = Строка.Сотрудник;
		МассивСотрудников = Новый Массив;
		МассивСотрудников.Добавить(Сотрудник);
		ПараметрыЗаполнения = РеквизитОбъект.ПолучитьДанныеПечати(МассивСотрудников);
		ПолеДокумент.Параметры.Заполнить(ПараметрыЗаполнения);
		ТабличныйДокумент.Вывести(ПолеДокумент);
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура Распечатать(Команда)
	НазваниеМакета = ЭтаФорма.Макет;
	Если Не ПустаяСтрока(НазваниеМакета) Тогда 
		Если Не ЗначениеЗаполнено(Объект.ДатаАктуальности) Тогда 
			Объект.ДатаАктуальности = КонецДня(ТекущаяДата());
		Иначе 
			Объект.ДатаАктуальности = КонецДня(Объект.ДатаАктуальности);
		КонецЕсли;
		ТабличныйДокумент = Новый ТабличныйДокумент;
		РаспечататьНаСервере(НазваниеМакета, ТабличныйДокумент);
		НайденныйМакет = СписокМакетов.НайтиПоЗначению(НазваниеМакета);
		Если НайденныйМакет <> Неопределено Тогда 
			Заголовок = НайденныйМакет.Представление;
		Иначе 
			Заголовок = "";
		КонецЕсли;
		ТабличныйДокумент.АвтоМасштаб = Истина;
		ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		ТабличныйДокумент.ТолькоПросмотр  = Истина;
		ТабличныйДокумент.ОтображатьЗаголовки = Ложь;
		ТабличныйДокумент.ОтображатьСетку = Ложь;
		ТабличныйДокумент.ДвусторонняяПечать = ТипДвустороннейПечати.ПереворотВлево;
		ТабличныйДокумент.Показать(Заголовок);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПриОткрытииНаСервере(СписокМакетов);
	Для Каждого ЭлементМакета Из СписокМакетов Цикл 
		Элементы.Макет.СписокВыбора.Добавить(ЭлементМакета.Значение,ЭлементМакета.Представление); 
	КонецЦикла;
	БезУволенных = Истина;
КонецПроцедуры

&НаСервере
Процедура ПриОткрытииНаСервере(СписокМакетов)
	РеквизитОбъект = РеквизитФормыВЗначение("Объект");
	СписокМакетов = Новый СписокЗначений;
	Для Каждого ЭлементМакетов Из РеквизитОбъект.Метаданные().Макеты Цикл 
		СписокМакетов.Добавить(ЭлементМакетов.Имя, ЭлементМакетов.Синоним);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСотрудников(Команда)
	ЗаполнитьСотрудниковНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСотрудниковНаСервере()
	СписокСотрудников.Очистить();
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТекущиеКадровыеДанныеСотрудников.Сотрудник,
	               |	ПриемНаРаботу.Ссылка,
	               |	ТекущиеКадровыеДанныеСотрудников.ТекущаяДолжность КАК Должность
	               |ИЗ
	               |	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриемНаРаботу КАК ПриемНаРаботу
	               |		ПО ТекущиеКадровыеДанныеСотрудников.Сотрудник = ПриемНаРаботу.Сотрудник
	               |ГДЕ
	               |	ТекущиеКадровыеДанныеСотрудников.ТекущееПодразделение = &ТекущееПодразделение
	               |	И НЕ ПриемНаРаботу.Ссылка ЕСТЬ NULL ";
				   
	Запрос.УстановитьПараметр("ТекущееПодразделение",Объект.Подразделение);				   
	Если БезУволенных Тогда 
		Запрос.Текст = Запрос.Текст + " И ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения = ДАТАВРЕМЯ(1, 1, 1)"
	КонецЕсли;
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда 
		Выгрузка = Результат.Выгрузить();
		сч=1;
		Для Каждого СтрокаВыгрузки ИЗ Выгрузка Цикл 
			НоваяСтрока = СписокСотрудников.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаВыгрузки);
			НоваяСтрока.НомерПП = сч;
			сч = сч+1;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	ЗаполнитьСотрудниковНаСервере();
КонецПроцедуры
