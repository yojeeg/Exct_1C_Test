
&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()
	ОтпускаБольничные.Очистить();
	Запрос = Новый Запрос;
	Если Отпуска Тогда 
		Запрос.Текст = "ВЫБРАТЬ
		|	Отпуск.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.Отпуск КАК Отпуск
		|ГДЕ
		|	Отпуск.ПланируемаяДатаВыплаты МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И Отпуск.Проведен = ИСТИНА";
		Запрос.УстановитьПараметр("ДатаНачала",НачалоДня(Объект.ДатаВыплаты));				   
		Запрос.УстановитьПараметр("ДатаОкончания",КонецДня(Объект.ДатаВыплаты));
	Иначе 
		Запрос.Текст = "ВЫБРАТЬ
		|	БольничныйЛист.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.БольничныйЛист КАК БольничныйЛист
		|ГДЕ
		|	БольничныйЛист.ПланируемаяДатаВыплаты МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И БольничныйЛист.Проведен = ИСТИНА";
		Запрос.УстановитьПараметр("ДатаНачала",НачалоДня(Объект.ДатаВыплаты));				   
		Запрос.УстановитьПараметр("ДатаОкончания",КонецДня(Объект.ДатаВыплаты));
	КонецЕсли;
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда 
		Выгрузка = Результат.Выгрузить();
		МассивСсылок = Выгрузка.ВыгрузитьКолонку("Ссылка");
		ОтпускаБольничные.ЗагрузитьЗначения(МассивСсылок);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СоздатьВедомостьНаСервере()
	Если ОтпускаБольничные.Количество()<>0 Тогда 
		НоваяВедомость = Документы.ВедомостьНаВыплатуЗарплатыВБанк.СоздатьДокумент();
		НоваяВедомость.Дата 						= ТекущаяДата();
		НоваяВедомость.ПериодРегистрации 			= НачалоМесяца(Объект.ДатаВыплаты);
		НоваяВедомость.Организация 					= Справочники.Организации.ОрганизацияПоУмолчанию();
		НоваяВедомость.ВыплатаЗарплатыВыполнена 	= Истина;
		Если Отпуска Тогда 
			НоваяВедомость.СпособВыплаты 				= Справочники.СпособыВыплатыЗарплаты.НайтиПоНаименованию("Отпуска");
		Иначе 
			НоваяВедомость.СпособВыплаты 				= Справочники.СпособыВыплатыЗарплаты.НайтиПоНаименованию("Больничные листы");
		КонецЕсли;
		НоваяВедомость.Ответственный 				= ПараметрыСеанса.ТекущийПользователь;
		НоваяВедомость.Округление					= Справочники.СпособыОкругленияПриРасчетеЗарплаты.НайтиПоНаименованию("Без округления");
		НоваяВедомость.ПеречислениеНДФЛВыполнено 	= Истина;
		НоваяВедомость.ПроцентВыплаты			 	= 100;
		Основания = НоваяВедомость.Основания;
		Для Каждого СтрокаОтпусковБольничных из ОтпускаБольничные Цикл 
			НоваяСтрока = Основания.Добавить();
			НоваяСтрока.Документ = СтрокаОтпусковБольничных.Значение;
		КонецЦикла;
		ЗаполнитьПодписантов(НоваяВедомость);		
		Попытка
			НоваяВедомость.Записать();		
			СообщениеПользователю = Новый СообщениеПользователю;
			СообщениеПользователю.Текст = "Создан документ " + НоваяВедомость.Ссылка;
			СообщениеПользователю.Сообщить();
		Исключение
			СообщениеПользователю = Новый СообщениеПользователю;
			СообщениеПользователю.Текст = ОписаниеОшибки();
			СообщениеПользователю.Сообщить();
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПодписантов(Ведомость)
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	СведенияОбОтветственныхЛицахСрезПоследних.Руководитель,
	               |	СведенияОбОтветственныхЛицахСрезПоследних.ДолжностьРуководителя,
	               |	СведенияОбОтветственныхЛицахСрезПоследних.ГлавныйБухгалтер
	               |ИЗ
	               |	РегистрСведений.СведенияОбОтветственныхЛицах.СрезПоследних КАК СведенияОбОтветственныхЛицахСрезПоследних";
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда 
		Выборка = Результат.Выбрать();
		Если Выборка.Следующий() Тогда 
			ЗаполнитьЗначенияСвойств(Ведомость, Выборка);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьВедомость(Команда)
	СоздатьВедомостьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура Отпуска1ПриИзменении(Элемент)
	Отпуска 	= Истина;
	Больничные 	= Ложь;
КонецПроцедуры

&НаКлиенте
Процедура БольничныеПриИзменении(Элемент)
	Больничные 	= Истина;
	Отпуска 	= Ложь;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Отпуска = Истина;
КонецПроцедуры
