Функция ПолучитьТаблицуКоманд()
	
	Команды = Новый ТаблицаЗначений;
	// Как будет выглядеть описание печатной формы для пользователя
	Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	// Имя нашего макета, что бы могли отличить вызванную команду в обработке печати
	Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	// Тут задается, как должна вызваться команда обработки
	// Возможные варианты:
	// - ОткрытиеФормы - в этом случае в колонке идентификатор должно быть указано имя формы, которое должна будет открыть система
	// - ВызовКлиентскогоМетода - вызвать клиентскую экспортную процедуру из модуля формы обработки
	// - ВызовСерверногоМетода - вызвать серверную экспортную процедуру из модуля объекта обработки
	Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));
	// Следующий параметр указывает, необходимо ли показывать оповещение при начале и завершению работы обработки. Не имеет смысла при открытии формы
	Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));
	// Для печатной формы должен содержать строку ПечатьMXL 
	Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
	
	Возврат Команды;
	
КонецФункции

//Создает в таблице команд новую строку

Функция ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
	НоваяКоманда = ТаблицаКоманд.Добавить();
	НоваяКоманда. Представление = Представление;
	НоваяКоманда. Идентификатор= Идентификатор;
	НоваяКоманда. Использование= Использование;
	НоваяКоманда. ПоказыватьОповещение= ПоказыватьОповещение;
	НоваяКоманда. Модификатор= Модификатор;
КонецФункции

Функция СведенияОВнешнейОбработке() Экспорт
	ПараметрыРегистрации = Новый Структура;
	МассивНазначений = Новый Массив;
	МассивНазначений.Добавить("Документ.ВозвратИзОтпускаПоУходуЗаРебенком");
	ПараметрыРегистрации.Вставить("Вид", "ПечатнаяФорма"); //может быть - ЗаполнениеОбъекта, ДополнительныйОтчет, СозданиеСвязанныхОбъектов... 
	ПараметрыРегистрации.Вставить("Назначение", МассивНазначений);
	ПараметрыРегистрации.Вставить("Наименование", "Приказ прервать отпуск по уходу за ребенком "); //имя под которым обработка будет зарегестрирована в справочнике внешних обработок
	ПараметрыРегистрации.Вставить("Версия", "1.0");
	ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);
	ПараметрыРегистрации.Вставить("Информация", " Приказ прервать отпуск по уходу за ребенком ");//так будет выглядеть описание печ.формы для пользователя
	ТаблицаКоманд = ПолучитьТаблицуКоманд();
	ДобавитьКоманду(ТаблицаКоманд, "ППФ Возврат из отпуска по уходу", "Макет", "ВызовСерверногоМетода", Истина);
	ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
	Возврат ПараметрыРегистрации;
КонецФункции

Процедура Печать(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Макет = ПолучитьМакет("Макет");
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Макет") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
		"Макет", "ППФ Возврат из отпуска по уходу",	ПолучитьТабличныйДокументПриказа(Макет,МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьТабличныйДокументПриказа(Макет, МассивОбъектов, ОбъектыПечати)
	
	ДокументРезультат = Новый ТабличныйДокумент;
	ДокументРезультат.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ВозвратИзОтпускаПоУходуЗаРебенком_ПриказОВыходеНаРаботуИзОтпускаПоУходуЗаРебенком";
	ДокументРезультат.ОриентацияСтраницы= ОриентацияСтраницы.Портрет;
	ДокументРезультат.АвтоМасштаб = Истина;
	
	МассивДанныхЗаполнения = ПолучитьДанныеДляПечатиКадровогоПриказаНаВозвратИзОТпуска(МассивОбъектов);	
	
	ПервыйДокумент = Истина;
	
	Для Каждого ДанныеПечати Из МассивДанныхЗаполнения Цикл
		
		// Документы нужно выводить на разных страницах.
		Если Не ПервыйДокумент Тогда
			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		Иначе
			ПервыйДокумент = Ложь;
		КонецЕсли;
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ДокументРезультат.ВысотаТаблицы + 1;

		Макет.Параметры.Заполнить(ДанныеПечати);
		
		ДокументРезультат.Вывести(Макет);
		
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ДокументРезультат, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
		
	КонецЦикла;
	
	Возврат ДокументРезультат;  

КонецФункции 

Функция ПолучитьДанныеДляПечатиКадровогоПриказаНаВозвратИзОТпуска(МассивОбъектов)
	
	МассивПараметров = Новый Массив;                    		
	Параметры = ПолучитьСтруктуруПараметровПриказа();		
	
	Запрос = Новый Запрос();
	Запрос.Текст  ="ВЫБРАТЬ
	               |	ВозвратИзОтпускаПоУходуЗаРебенком.Сотрудник,
	               |	ВозвратИзОтпускаПоУходуЗаРебенком.ДатаВозврата,
	               |	КадроваяИсторияСотрудниковСрезПоследних.Организация.КодПоОКПО КАК КодПоОКПО,
	               |	КадроваяИсторияСотрудниковСрезПоследних.Должность КАК ДолжностьБезПадежа,
	               |	КадроваяИсторияСотрудниковСрезПоследних.Подразделение,
	               |	ВЫБОР
	               |		КОГДА (ВЫРАЗИТЬ(КадроваяИсторияСотрудниковСрезПоследних.Организация.НаименованиеПолное КАК СТРОКА(1))) = """"""""
	               |			ТОГДА ВЫБОР
	               |					КОГДА (ВЫРАЗИТЬ(КадроваяИсторияСотрудниковСрезПоследних.Организация.НаименованиеСокращенное КАК СТРОКА(1))) = """"""""
	               |						ТОГДА КадроваяИсторияСотрудниковСрезПоследних.Организация.Наименование
	               |					ИНАЧЕ КадроваяИсторияСотрудниковСрезПоследних.Организация.НаименованиеСокращенное
	               |				КОНЕЦ
	               |		ИНАЧЕ КадроваяИсторияСотрудниковСрезПоследних.Организация.НаименованиеПолное
	               |	КОНЕЦ КАК НазваниеОрганизации,
	               |	ВозвратИзОтпускаПоУходуЗаРебенком.Номер КАК НомерДок,
	               |	ВозвратИзОтпускаПоУходуЗаРебенком.Дата КАК ДатаДок,
	               |	ЕСТЬNULL(ФИОФизическихЛицСрезПоследних.Фамилия, """") КАК Фамилия,
	               |	ЕСТЬNULL(ФИОФизическихЛицСрезПоследних.Имя, """") КАК Имя,
	               |	ЕСТЬNULL(ФИОФизическихЛицСрезПоследних.Отчество, """") КАК Отчество,
	               |	ВозвратИзОтпускаПоУходуЗаРебенком.Ссылка,
	               |	ВЫБОР
	               |		КОГДА ВозвратИзОтпускаПоУходуЗаРебенком.Сотрудник.Пол = ЗНАЧЕНИЕ(Перечисление.ПолФизическогоЛица.Мужской)
	               |			ТОГДА 1
	               |		ИНАЧЕ 2
	               |	КОНЕЦ КАК Пол,
	               |	ДополнительныеСведения.Значение КАК Должность
	               |ИЗ
	               |	Документ.ВозвратИзОтпускаПоУходуЗаРебенком КАК ВозвратИзОтпускаПоУходуЗаРебенком
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизическихЛиц.СрезПоследних(&ДатаСреза, ) КАК ФИОФизическихЛицСрезПоследних
	               |		ПО ВозвратИзОтпускаПоУходуЗаРебенком.Сотрудник = ФИОФизическихЛицСрезПоследних.ФизическоеЛицо
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КадроваяИсторияСотрудников.СрезПоследних(&ДатаСреза, ) КАК КадроваяИсторияСотрудниковСрезПоследних
	               |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	               |			ПО КадроваяИсторияСотрудниковСрезПоследних.Должность = ДополнительныеСведения.Объект
	               |		ПО ВозвратИзОтпускаПоУходуЗаРебенком.Сотрудник = КадроваяИсторияСотрудниковСрезПоследних.ФизическоеЛицо
	               |ГДЕ
	               |	ВозвратИзОтпускаПоУходуЗаРебенком.Ссылка = &Ссылка
	               |	И ДополнительныеСведения.Свойство = &Свойство";
	Запрос.УстановитьПараметр("Ссылка",МассивОбъектов[0]);				   
	Запрос.УстановитьПараметр("ДатаСреза",МассивОбъектов[0].ДатаВозврата);				   
	Запрос.УстановитьПараметр("Свойство",ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию("Дательный падеж (Должности)"));
	Результат 	= Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда 	
		
		Выборка 	= Результат.Выбрать();
		Если Выборка.Следующий() Тогда 
			
			ЗаполнитьЗначенияСвойств(Параметры,Выборка);			
			Параметры.ДатаВозврата = Формат(Выборка.ДатаВозврата, "ДЛФ=Д");
			Параметры.НомерДок = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Параметры.НомерДок, Истина, Истина);

			ФИО = "";
			Если ФизическиеЛицаЗарплатаКадры.Просклонять(Выборка.Фамилия +" "+ Выборка.Имя +" "+ Выборка.Отчество, 3, ФИО, Выборка.Пол) Тогда
				Параметры.ФИО = ФИО;
			КонецЕсли; 		
			
			// Подразделение
			Если Параметры.Свойство("Подразделение") Тогда 
				Подразделение = Выборка.Подразделение;
				
				ЗапросПоПодразделениям = Новый Запрос;
				ТекстЗапроса = "ВЫБРАТЬ
				|	ДополнительныеСведения.Объект КАК Объект,
				|	ДополнительныеСведения.Свойство КАК Свойство,
				|	ДополнительныеСведения.Значение КАК Значение,
				|	""1"" КАК УровеньВложенности
				|ИЗ
				|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
				|ГДЕ
				|	ДополнительныеСведения.Объект = &Ссылка
				|ОБЪЕДИНИТЬ ВСЕ                             	
				|ВЫБРАТЬ
				|	ДополнительныеСведения.Объект,
				|	ДополнительныеСведения.Свойство,
				|	ДополнительныеСведения.Значение,
				|	""2""
				|ИЗ
				|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
				|ГДЕ
				|	ДополнительныеСведения.Объект = &СсылкаРодитель
				
				|ОБЪЕДИНИТЬ ВСЕ
				
				|ВЫБРАТЬ
				|	ДополнительныеСведения.Объект,
				|	ДополнительныеСведения.Свойство,
				|	ДополнительныеСведения.Значение,
				|	""3""
				|ИЗ
				|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
				|ГДЕ
				|	ДополнительныеСведения.Объект = &СсылкаРодительРодитель";
				ЗапросПоПодразделениям.Текст = ТекстЗапроса;
				ЗапросПоПодразделениям.УстановитьПараметр("Ссылка",Подразделение);
				ЗапросПоПодразделениям.УстановитьПараметр("СсылкаРодитель",Подразделение.Родитель);
				ЗапросПоПодразделениям.УстановитьПараметр("СсылкаРодительРодитель",Подразделение.Родитель.Родитель);
				
				ВыгрузкаПодразделений = ЗапросПоПодразделениям.Выполнить().Выгрузить();
				
				СтрокаПодразделения = "";
				МассивУровней = Новый Массив;
				МассивУровней.Добавить("1");
				МассивУровней.Добавить("2");
				МассивУровней.Добавить("3");
				Для Каждого УровеньВложенности из МассивУровней Цикл 
					Отбор = Новый Структура("УровеньВложенности",УровеньВложенности);
					ТаблицаСОтбором = ВыгрузкаПодразделений.Скопировать(Отбор);
					Если УровеньВложенности = "1" И ТаблицаСОтбором.Количество()=0 Тогда 
						СтрокаПодразделения = Подразделение;
						Прервать;
					КонецЕсли;
					
					//Если УровеньВложенности = "1" Тогда 
					//	Для Каждого Строка из ТаблицаСОтбором Цикл 
					//		Если Строка.Свойство.Заголовок = "Винительный падеж" Тогда 
					//			СтрокаПодразделения = СтрокаПодразделения + " " + Строка.Значение;
					//		КонецЕсли;
					//	КонецЦикла;
					//Иначе 
						Для Каждого Строка из ТаблицаСОтбором Цикл 
							Если Строка.Свойство.Заголовок = "Родительный падеж" Тогда 
								СтрокаПодразделения = СтрокаПодразделения + " " + Строка.Значение;
							КонецЕсли;
						КонецЦикла;	
					//КонецЕсли;
				КонецЦикла;
				
				Параметры.Подразделение = СокрЛП(СтрокаПодразделения);
				
			КонецЕсли;
			
			ФИО = "";
			Если ФизическиеЛицаЗарплатаКадры.Просклонять(Выборка.Фамилия +" "+ Выборка.Имя +" "+ Выборка.Отчество, 2, ФИО, Выборка.Пол) Тогда
				МассивСтрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ФИО, " ");
				ФамилияИО = "";
				сч=1;
				Для Каждого ЭлементМассива Из МассивСтрок Цикл 
					Если сч=1 Тогда 
						ФамилияИО = ЭлементМассива;
					Иначе 
						ФамилияИО = ФамилияИО + " " + Лев(ЭлементМассива,1)+"."
					КонецЕсли;
					сч = сч + 1;
				КонецЦикла;
				Параметры.ФамилияИО = ФамилияИО;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	МассивПараметров.Добавить(Параметры);
	
	Возврат МассивПараметров;
КонецФункции

Функция ПолучитьСтруктуруПараметровПриказа()
	Параметры = Новый Структура;
	
	Параметры.Вставить("Ссылка");
	Параметры.Вставить("ДатаВозврата");
	Параметры.Вставить("НомерДок");
	Параметры.Вставить("ДатаДок");
	Параметры.Вставить("КодПоОКПО");
	Параметры.Вставить("НазваниеОрганизации");
	Параметры.Вставить("ФИО");
	Параметры.Вставить("Должность");
	Параметры.Вставить("Подразделение");
	Параметры.Вставить("ФамилияИО");

	Возврат Параметры;
	
КонецФункции	
