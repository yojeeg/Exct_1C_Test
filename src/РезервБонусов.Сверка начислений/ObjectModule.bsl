////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

#Область ПрограммныйИнтерфейс

// Возвращает сведения о внешней обработке.
Функция СведенияОВнешнейОбработке() Экспорт
	
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке("2.2.2.4");
	
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиДополнительныйОтчет();
	ПараметрыРегистрации.Версия = "1.2";
	ПараметрыРегистрации.ОпределитьНастройкиФормы = Истина;
	
	НоваяКоманда = ПараметрыРегистрации.Команды.Добавить();
	НоваяКоманда.Представление = НСтр("ru = 'Резерв бонусов.Сверка. Устаревшая'");
	НоваяКоманда.Идентификатор = "СверкаРезерваБонусовУстаревшая";
	НоваяКоманда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыОткрытиеФормы();
	НоваяКоманда.ПоказыватьОповещение = Ложь;
	
	Возврат ПараметрыРегистрации;
	
КонецФункции
#КонецОбласти


Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	СхемаКомпоновкиДанных = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	Настройки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);
	
	ИтоговаяТаблица = ПолучитьИтоговуюТаблицу();
	
	ВнешнийНаборДанных = Новый Структура;
	ВнешнийНаборДанных.Вставить("ИтоговаяТаблица", ИтоговаяТаблица);
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешнийНаборДанных, ДанныеРасшифровки);
	
	ДокументРезультат.Очистить();
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
КонецПроцедуры

Функция ПолучитьИтоговуюТаблицу()
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	СостоянияСотрудников.Сотрудник КАК Сотрудник,
	               |	МАКСИМУМ(СостоянияСотрудников.Период) КАК Период
	               |ПОМЕСТИТЬ ВТ_ПериодыСостояний
	               |ИЗ
	               |	РегистрСведений.СостоянияСотрудников КАК СостоянияСотрудников
	               |ГДЕ
	               |	СостоянияСотрудников.Период <= &КонецПериода
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СостоянияСотрудников.Сотрудник
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ПериодыСостояний.Сотрудник КАК Сотрудник,
	               |	ВТ_ПериодыСостояний.Период КАК Период,
	               |	СостоянияСотрудников.Состояние КАК Состояние
	               |ПОМЕСТИТЬ ВТ_СОСТОЯНИЯ_СОТРУДНИКОВ
	               |ИЗ
	               |	ВТ_ПериодыСостояний КАК ВТ_ПериодыСостояний
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияСотрудников КАК СостоянияСотрудников
	               |		ПО ВТ_ПериодыСостояний.Сотрудник = СостоянияСотрудников.Сотрудник
	               |			И (СостоянияСотрудников.Период = ВТ_ПериодыСостояний.Период)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ППФ_РезервБонусов.Сотрудник
	               |ПОМЕСТИТЬ ВТ_СОТРУДНИКИ
	               |ИЗ
	               |	РегистрСведений.ППФ_РезервБонусов КАК ППФ_РезервБонусов
	               |ГДЕ
	               |	ППФ_РезервБонусов.Период МЕЖДУ &НачалоПериода И &КонецПериода
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СальдоНаНачалоПериода.Сотрудник КАК Сотрудник,
	               |	СальдоНаНачалоПериода.РезервКонецПериода КАК РезервНачало,
	               |	СальдоНаНачалоПериода.РезервКонецПериода_ОПС КАК РезервНачало_ОПС,
	               |	СальдоНаНачалоПериода.РезервКонецПериода_ФОМС КАК РезервНачало_ФОМС,
	               |	СальдоНаНачалоПериода.РезервКонецПериода_ФСС КАК РезервНачало_ФСС,
	               |	СальдоНаНачалоПериода.РезервКонецПериода_НС КАК РезервНачало_НС
	               |ПОМЕСТИТЬ ВТ_НачальноеСальдо
	               |ИЗ
	               |	РегистрСведений.ППФ_РезервБонусов.СрезПоследних(&НачалоПериода, Период >= &НачалоМесяцаНачалаПериода) КАК СальдоНаНачалоПериода
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ППФ_РезервБонусовСрезПоследних.Сотрудник КАК Сотрудник,
	               |	ППФ_РезервБонусовСрезПоследних.РезервКонецПериода КАК РезервКонецПериода,
	               |	ППФ_РезервБонусовСрезПоследних.VP КАК VP,
	               |	ППФ_РезервБонусовСрезПоследних.Период,
	               |	ППФ_РезервБонусовСрезПоследних.РезервКонецПериода_ОПС,
	               |	ППФ_РезервБонусовСрезПоследних.РезервКонецПериода_ФОМС,
	               |	ППФ_РезервБонусовСрезПоследних.РезервКонецПериода_ФСС,
	               |	ППФ_РезервБонусовСрезПоследних.РезервКонецПериода_НС,
	               |	ППФ_РезервБонусовСрезПоследних.VP_ОПС,
	               |	ППФ_РезервБонусовСрезПоследних.VP_ФОМС,
	               |	ППФ_РезервБонусовСрезПоследних.VP_ФСС,
	               |	ППФ_РезервБонусовСрезПоследних.VP_НС,
	               |	КадроваяИсторияСотрудниковСрезПоследних.Подразделение
	               |ПОМЕСТИТЬ ВТ_КонечноСальдо
	               |ИЗ
	               |	РегистрСведений.ППФ_РезервБонусов.СрезПоследних(&КонецПериода, Период >= &НачалоМесяцаКонцаПериода) КАК ППФ_РезервБонусовСрезПоследних
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КадроваяИсторияСотрудников.СрезПоследних(&КонецПериода, ) КАК КадроваяИсторияСотрудниковСрезПоследних
	               |		ПО ППФ_РезервБонусовСрезПоследних.Сотрудник = КадроваяИсторияСотрудниковСрезПоследних.Сотрудник
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_КонечноСальдо.Сотрудник.Код КАК ТабНомер,
	               |	ВТ_КонечноСальдо.Сотрудник КАК Сотрудник,
	               |	ВТ_КонечноСальдо.Сотрудник.Наименование КАК СотрудникНаименование,
	               |	ВТ_НачальноеСальдо.РезервНачало КАК РезервНачало,
	               |	ВТ_КонечноСальдо.VP КАК VP,
	               |	ВТ_КонечноСальдо.РезервКонецПериода КАК РезервКонец,
	               |	ЕСТЬNULL(ВТ_КонечноСальдо.РезервКонецПериода, 0) - ЕСТЬNULL(ВТ_НачальноеСальдо.РезервНачало, 0) + ЕСТЬNULL(ВТ_КонечноСальдо.VP, 0) КАК Начисление,
	               |	ВТ_НачальноеСальдо.РезервНачало_ОПС,
	               |	ВТ_НачальноеСальдо.РезервНачало_ФОМС,
	               |	ВТ_НачальноеСальдо.РезервНачало_ФСС,
	               |	ВТ_НачальноеСальдо.РезервНачало_НС,
	               |	ВТ_КонечноСальдо.VP_ОПС,
	               |	ВТ_КонечноСальдо.VP_ФОМС,
	               |	ВТ_КонечноСальдо.VP_ФСС,
	               |	ВТ_КонечноСальдо.VP_НС,
	               |	ВТ_КонечноСальдо.РезервКонецПериода_ОПС,
	               |	ВТ_КонечноСальдо.РезервКонецПериода_ФОМС,
	               |	ВТ_КонечноСальдо.РезервКонецПериода_ФСС,
	               |	ВТ_КонечноСальдо.РезервКонецПериода_НС,
	               |	ЕСТЬNULL(ВТ_КонечноСальдо.РезервКонецПериода_ОПС, 0) - ЕСТЬNULL(ВТ_НачальноеСальдо.РезервНачало_ОПС, 0) + ЕСТЬNULL(ВТ_КонечноСальдо.VP_ОПС, 0) КАК НачислениеОПС,
	               |	ЕСТЬNULL(ВТ_КонечноСальдо.РезервКонецПериода_ФОМС, 0) - ЕСТЬNULL(ВТ_НачальноеСальдо.РезервНачало_ФОМС, 0) + ЕСТЬNULL(ВТ_КонечноСальдо.VP_ФОМС, 0) КАК НачислениеФОМС,
	               |	ЕСТЬNULL(ВТ_КонечноСальдо.РезервКонецПериода_ФСС, 0) - ЕСТЬNULL(ВТ_НачальноеСальдо.РезервНачало_ФСС, 0) + ЕСТЬNULL(ВТ_КонечноСальдо.VP_ФСС, 0) КАК НачислениеФСС,
	               |	ЕСТЬNULL(ВТ_КонечноСальдо.РезервКонецПериода_НС, 0) - ЕСТЬNULL(ВТ_НачальноеСальдо.РезервНачало_НС, 0) + ЕСТЬNULL(ВТ_КонечноСальдо.VP_НС, 0) КАК НачислениеНС,
	               |	ППФ_СоответствиеПодразделенийИМВЗСрезПоследних.МВЗ,
	               |	ППФ_СоответствиеПодразделенийИМВЗСрезПоследних.МВЗ.Код КАК КодМВЗ,
	               |	ВТ_СОСТОЯНИЯ_СОТРУДНИКОВ.Состояние КАК Состояние
	               |ПОМЕСТИТЬ ВТ_Промежуточная
	               |ИЗ
	               |	ВТ_КонечноСальдо КАК ВТ_КонечноСальдо
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НачальноеСальдо КАК ВТ_НачальноеСальдо
	               |		ПО ВТ_КонечноСальдо.Сотрудник = ВТ_НачальноеСальдо.Сотрудник
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ППФ_СоответствиеПодразделенийИМВЗ.СрезПоследних(&КонецПериода, ) КАК ППФ_СоответствиеПодразделенийИМВЗСрезПоследних
	               |		ПО ВТ_КонечноСальдо.Подразделение = ППФ_СоответствиеПодразделенийИМВЗСрезПоследних.Подразделение
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СОСТОЯНИЯ_СОТРУДНИКОВ КАК ВТ_СОСТОЯНИЯ_СОТРУДНИКОВ
	               |		ПО ВТ_КонечноСальдо.Сотрудник = ВТ_СОСТОЯНИЯ_СОТРУДНИКОВ.Сотрудник
	               |ГДЕ
	               |	ВТ_КонечноСальдо.Сотрудник В
	               |			(ВЫБРАТЬ
	               |				ВТ_СОТРУДНИКИ.Сотрудник
	               |			ИЗ
	               |				ВТ_СОТРУДНИКИ КАК ВТ_СОТРУДНИКИ)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_Промежуточная.ТабНомер КАК ТабНомер,
	               |	ВТ_Промежуточная.Сотрудник КАК Сотрудник,
	               |	ВТ_Промежуточная.РезервНачало КАК РезервНачало,
	               |	ВТ_Промежуточная.VP КАК VP,
	               |	ВТ_Промежуточная.РезервКонец КАК РезервКонец,
	               |	ВТ_Промежуточная.Начисление КАК Начисление,				   
				   //|	ВЫБОР
				   //|		КОГДА ВТ_Промежуточная.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияСотрудника.ОтпускПоУходуЗаРебенком)
				   //|				ИЛИ ВТ_Промежуточная.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияСотрудника.ОтпускПоБеременностиИРодам)
				   //|			ТОГДА 0
				   //|		ИНАЧЕ ВТ_Промежуточная.Начисление
				   //|	КОНЕЦ КАК Начисление,
	               |	ВТ_Промежуточная.РезервНачало_ОПС КАК РезервНачало_ОПС,
	               |	ВТ_Промежуточная.РезервНачало_ФОМС КАК РезервНачало_ФОМС,
	               |	ВТ_Промежуточная.РезервНачало_ФСС КАК РезервНачало_ФСС,
	               |	ВТ_Промежуточная.РезервНачало_НС КАК РезервНачало_НС,
	               |	ВТ_Промежуточная.VP_ОПС КАК VP_ОПС,
	               |	ВТ_Промежуточная.VP_ФОМС КАК VP_ФОМС,
	               |	ВТ_Промежуточная.VP_ФСС КАК VP_ФСС,
	               |	ВТ_Промежуточная.VP_НС КАК VP_НС,
	               |	ВТ_Промежуточная.РезервКонецПериода_ОПС КАК РезервКонецПериода_ОПС,
	               |	ВТ_Промежуточная.РезервКонецПериода_ФОМС КАК РезервКонецПериода_ФОМС,
	               |	ВТ_Промежуточная.РезервКонецПериода_ФСС КАК РезервКонецПериода_ФСС,
	               |	ВТ_Промежуточная.РезервКонецПериода_НС КАК РезервКонецПериода_НС,
	               |	ВТ_Промежуточная.НачислениеОПС КАК НачислениеОПС,
	               |	ВТ_Промежуточная.НачислениеФОМС КАК НачислениеФОМС,
	               |	ВТ_Промежуточная.НачислениеФСС КАК НачислениеФСС,
	               |	ВТ_Промежуточная.НачислениеНС КАК НачислениеНС,
				   
				   //|	ВЫБОР
				   //|		КОГДА ВТ_Промежуточная.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияСотрудника.ОтпускПоУходуЗаРебенком)
				   //|				ИЛИ ВТ_Промежуточная.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияСотрудника.ОтпускПоБеременностиИРодам)
				   //|			ТОГДА 0
				   //|		ИНАЧЕ ВТ_Промежуточная.НачислениеОПС
				   //|	КОНЕЦ КАК НачислениеОПС,
				   //|	ВЫБОР
				   //|		КОГДА ВТ_Промежуточная.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияСотрудника.ОтпускПоУходуЗаРебенком)
				   //|				ИЛИ ВТ_Промежуточная.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияСотрудника.ОтпускПоБеременностиИРодам)
				   //|			ТОГДА 0
				   //|		ИНАЧЕ ВТ_Промежуточная.НачислениеФОМС
				   //|	КОНЕЦ КАК НачислениеФОМС,
				   //|	ВЫБОР
				   //|		КОГДА ВТ_Промежуточная.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияСотрудника.ОтпускПоУходуЗаРебенком)
				   //|				ИЛИ ВТ_Промежуточная.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияСотрудника.ОтпускПоБеременностиИРодам)
				   //|			ТОГДА 0
				   //|		ИНАЧЕ ВТ_Промежуточная.НачислениеФСС
				   //|	КОНЕЦ КАК НачислениеФСС,
				   //|	ВЫБОР
				   //|		КОГДА ВТ_Промежуточная.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияСотрудника.ОтпускПоУходуЗаРебенком)
				   //|				ИЛИ ВТ_Промежуточная.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияСотрудника.ОтпускПоБеременностиИРодам)
				   //|			ТОГДА 0
				   //|		ИНАЧЕ ВТ_Промежуточная.НачислениеНС
				   //|	КОНЕЦ КАК НачислениеНС,
	               |	ВТ_Промежуточная.МВЗ КАК МВЗ,
	               |	ВТ_Промежуточная.КодМВЗ КАК КодМВЗ,
	               |	ВТ_Промежуточная.СотрудникНаименование КАК СотрудникНаименование
	               |ИЗ
	               |	ВТ_Промежуточная КАК ВТ_Промежуточная
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	СотрудникНаименование
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ ВТ_НачальноеСальдо
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ ВТ_КонечноСальдо";
	Запрос.УстановитьПараметр("НачалоПериода",	КонецДня(НачалоМесяца(ЭтотОбъект.НачалоПериода)-1));				   
	Запрос.УстановитьПараметр("КонецПериода",	КонецДня(КонецМесяца(ЭтотОбъект.КонецПериода)));
	Запрос.УстановитьПараметр("НачалоМесяцаНачалаПериода",	НачалоМесяца(КонецДня(НачалоМесяца(ЭтотОбъект.НачалоПериода)-1)));
	Запрос.УстановитьПараметр("НачалоМесяцаКонцаПериода",	НачалоМесяца(КонецДня(КонецМесяца(ЭтотОбъект.КонецПериода))));
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

