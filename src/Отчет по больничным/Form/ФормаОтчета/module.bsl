////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	НачПериода = НачалоМесяца(ТекущаяДата());
	КонПериода = КонецМесяца(ТекущаяДата());
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура КнопкаСформироватьНажатие(Кнопка)
	ТабДок = ЭлементыФормы.ПолеТабличногоДокумента1;
	
	ТабДок.Очистить();
	
	Макет = ПолучитьМакет("Макет");
	
	ОблШапка = Макет.ПолучитьОбласть("Шапка");
	ОблШапка.Параметры.НачПериода = Формат(НачПериода, "ДЛФ=DD");
	ОблШапка.Параметры.КонПериода = Формат(КонПериода, "ДЛФ=DD");

	ТабДок.Вывести(ОблШапка);
	
	ОблШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ТабДок.Вывести(ОблШапкаТаблицы);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НачислениеПоБольничномуЛистуНачисления.Ссылка,
	|	НачислениеПоБольничномуЛистуНачисления.Сотрудник,
	|	НачислениеПоБольничномуЛистуНачисления.ВидРасчета,
	|	СУММА(НачислениеПоБольничномуЛистуНачисления.Результат) КАК Результат,
	|	СУММА(НачислениеПоБольничномуЛистуНачисления.ОплаченоДнейЧасов) КАК ОплаченоДнейЧасов
	|ПОМЕСТИТЬ ВТ_НАЧИСЛЕНИЕ_БЕРЕМЕННОСТЬ_И_РОДЫ
	|ИЗ
	|	Документ.НачислениеПоБольничномуЛисту.Начисления КАК НачислениеПоБольничномуЛистуНачисления
	|ГДЕ
	|	НачислениеПоБольничномуЛистуНачисления.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.ОтпускПоБеременностиИРодам)
	|	И НачислениеПоБольничномуЛистуНачисления.Ссылка.Проведен = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	НачислениеПоБольничномуЛистуНачисления.Ссылка,
	|	НачислениеПоБольничномуЛистуНачисления.Сотрудник,
	|	НачислениеПоБольничномуЛистуНачисления.ВидРасчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачислениеПоБольничномуЛистуНачисления.Сотрудник КАК Сотрудник,
	|	НачислениеПоБольничномуЛистуНачисления.Ссылка КАК Ссылка,
	|	НачислениеПоБольничномуЛистуНачисления.ВидРасчета,
	|	СУММА(НачислениеПоБольничномуЛистуНачисления.Результат) КАК Результат,
	|	НачислениеПоБольничномуЛистуНачисления.ПодразделениеОрганизации,
	|	СУММА(НачислениеПоБольничномуЛистуНачисления.ОплаченоДнейЧасов) КАК ОплаченоДнейЧасов
	|ПОМЕСТИТЬ ВТ_НАЧИСЛЕНИЯ
	|ИЗ
	|	Документ.НачислениеПоБольничномуЛисту.Начисления КАК НачислениеПоБольничномуЛистуНачисления
	|ГДЕ
	|	НачислениеПоБольничномуЛистуНачисления.ВидРасчета <> ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.ОкладПоДням)
	//|	И НачислениеПоБольничномуЛистуНачисления.Сторно = ЛОЖЬ
	|
	|СГРУППИРОВАТЬ ПО
	|	НачислениеПоБольничномуЛистуНачисления.Сотрудник,
	|	НачислениеПоБольничномуЛистуНачисления.Ссылка,
	|	НачислениеПоБольничномуЛистуНачисления.ВидРасчета,
	|	НачислениеПоБольничномуЛистуНачисления.ПодразделениеОрганизации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачислениеПоБольничномуЛисту.Ссылка,
	|	НачислениеПоБольничномуЛисту.Дата КАК Дата,
	|	НачислениеПоБольничномуЛисту.Номер,
	|	НачислениеПоБольничномуЛисту.Сотрудник КАК Сотрудник,
	|	НачислениеПоБольничномуЛисту.Сотрудник.Код КАК ТабельныйНомер,
	|	НачислениеПоБольничномуЛисту.ДатаНачала КАК НачалоПериода,
	|	НачислениеПоБольничномуЛисту.ДатаОкончания КАК КонецПериода,
	|	НачислениеПоБольничномуЛисту.ПериодРегистрации КАК МесяцНачисления,
	|	НачислениеПоБольничномуЛисту.ПервичныйБольничныйЛист,
	|	НачислениеПоБольничномуЛисту.СерияВходящегоДокумента,
	|	НачислениеПоБольничномуЛисту.НомерВходящегоДокумента,
	|	НачислениеПоБольничномуЛисту.ПричинаНетрудоспособности,
	|	НачислениеПоБольничномуЛисту.ПроцентОплаты,
	|	ЕСТЬNULL(ВТ_НАЧИСЛЕНИЯ_1.Результат, 0) + ЕСТЬNULL(ВТ_НАЧИСЛЕНИЕ_БЕРЕМЕННОСТЬ_И_РОДЫ.Результат, 0) + ЕСТЬNULL(ВТ_НАЧИСЛЕНИЯ_2.Результат, 0) КАК БольничныйВсего,
	|	ЕСТЬNULL(ВТ_НАЧИСЛЕНИЯ_1.Результат, 0) + ЕСТЬNULL(ВТ_НАЧИСЛЕНИЕ_БЕРЕМЕННОСТЬ_И_РОДЫ.Результат, 0) КАК ЗаСчетФСС,
	|	ЕСТЬNULL(ВТ_НАЧИСЛЕНИЯ_2.Результат, 0) КАК ЗаСчетРаботодателяПо202ФЗ,
	|	ЕСТЬNULL(ВТ_НАЧИСЛЕНИЯ_2.ОплаченоДнейЧасов, 0) + ЕСТЬNULL(ВТ_НАЧИСЛЕНИЯ_1.ОплаченоДнейЧасов, 0) + ЕСТЬNULL(ВТ_НАЧИСЛЕНИЕ_БЕРЕМЕННОСТЬ_И_РОДЫ.ОплаченоДнейЧасов, 0) КАК ОплаченоДнейБольничныйВсего,
	|	ЕСТЬNULL(ВТ_НАЧИСЛЕНИЯ_2.ОплаченоДнейЧасов, 0) КАК ОплаченоДнейЗаСчетРаботодателяПо202ФЗ,
	|	ЕСТЬNULL(ВТ_НАЧИСЛЕНИЯ_1.ОплаченоДнейЧасов, 0) + ЕСТЬNULL(ВТ_НАЧИСЛЕНИЕ_БЕРЕМЕННОСТЬ_И_РОДЫ.ОплаченоДнейЧасов, 0) КАК ОплаченоДнейЗаСчетФСС,
	|	ВЫБОР
	|		КОГДА НачислениеПоБольничномуЛисту.ДатаНарушенияРежима <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА РАЗНОСТЬДАТ(НачислениеПоБольничномуЛисту.ДатаНарушенияРежима, НачислениеПоБольничномуЛисту.ДатаОкончания, ДЕНЬ) + 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ДнейСНарушениемРежима,
	|	НачислениеПоБольничномуЛисту.ДатаНарушенияРежима КАК НарушениеРежимаС
	|ИЗ
	|	Документ.НачислениеПоБольничномуЛисту КАК НачислениеПоБольничномуЛисту
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НАЧИСЛЕНИЯ КАК ВТ_НАЧИСЛЕНИЯ_1
	|		ПО НачислениеПоБольничномуЛисту.Ссылка = ВТ_НАЧИСЛЕНИЯ_1.Ссылка
	|			И НачислениеПоБольничномуЛисту.Сотрудник = ВТ_НАЧИСЛЕНИЯ_1.Сотрудник
	|			И (ВТ_НАЧИСЛЕНИЯ_1.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.ОплатаПоСреднемуБЛ))
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НАЧИСЛЕНИЯ КАК ВТ_НАЧИСЛЕНИЯ_2
	|		ПО НачислениеПоБольничномуЛисту.Ссылка = ВТ_НАЧИСЛЕНИЯ_2.Ссылка
	|			И НачислениеПоБольничномуЛисту.Сотрудник = ВТ_НАЧИСЛЕНИЯ_2.Сотрудник
	|			И (ВТ_НАЧИСЛЕНИЯ_2.ВидРасчета = &ВидРасчетаОплатаБольничныхЛистов2)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НАЧИСЛЕНИЕ_БЕРЕМЕННОСТЬ_И_РОДЫ КАК ВТ_НАЧИСЛЕНИЕ_БЕРЕМЕННОСТЬ_И_РОДЫ
	|		ПО НачислениеПоБольничномуЛисту.Ссылка = ВТ_НАЧИСЛЕНИЕ_БЕРЕМЕННОСТЬ_И_РОДЫ.Ссылка
	|			И НачислениеПоБольничномуЛисту.Сотрудник = ВТ_НАЧИСЛЕНИЕ_БЕРЕМЕННОСТЬ_И_РОДЫ.Сотрудник
	|ГДЕ
	|	НачислениеПоБольничномуЛисту.Проведен = ИСТИНА
	|	И НачислениеПоБольничномуЛисту.Проведен = ИСТИНА
	|	И НачислениеПоБольничномуЛисту.Дата >= &ДатаНачала
	|	И НачислениеПоБольничномуЛисту.Дата <= &ДатаОкончания
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудник
	|АВТОУПОРЯДОЧИВАНИЕ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_НАЧИСЛЕНИЯ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_НАЧИСЛЕНИЕ_БЕРЕМЕННОСТЬ_И_РОДЫ";
	
	Запрос.УстановитьПараметр("ВидРасчетаОплатаБольничныхЛистов2", ПолучитьПланВидовРасчетаОплатаБольничныхЛистовЗаСчетРаботодателя());
	Запрос.УстановитьПараметр("ДатаНачала", НачалоДня(НачПериода));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(КонПериода));
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		ОблСтрокаТаблицы = Макет.ПолучитьОбласть("СтрокаТаблицы");
		ПорядковыйНомер = 1;
		ИтогоБольничныйВсего = 0;
		ИтогоЗаСчетФСС = 0;
		ИтогоЗаСчетРаботодателяПо202ФЗ = 0;
		ИтогоОплаченоДнейЗаСчетРаботодателяПо202ФЗ = 0;
		ИтогоОплаченоДнейЗаСчетФСС = 0;
		ИтогоОплаченоДнейБольничныйВсего = 0;
		ИтогоДнейСНарушениемРежима = 0;
		Пока Выборка.Следующий() Цикл
			ОблСтрокаТаблицы.Параметры.Заполнить(Выборка);
			ОблСтрокаТаблицы.Параметры.ПорядковыйНомер = ПорядковыйНомер;
			ТабДок.Вывести(ОблСтрокаТаблицы);
			ПорядковыйНомер = ПорядковыйНомер + 1;
			ИтогоБольничныйВсего = ИтогоБольничныйВсего + Выборка.БольничныйВсего;
			ИтогоЗаСчетФСС = ИтогоЗаСчетФСС + Выборка.ЗаСчетФСС;
			ИтогоЗаСчетРаботодателяПо202ФЗ = ИтогоЗаСчетРаботодателяПо202ФЗ + Выборка.ЗаСчетРаботодателяПо202ФЗ;
			ИтогоОплаченоДнейЗаСчетРаботодателяПо202ФЗ = ИтогоОплаченоДнейЗаСчетРаботодателяПо202ФЗ + Выборка.ОплаченоДнейЗаСчетРаботодателяПо202ФЗ;
			ИтогоОплаченоДнейЗаСчетФСС = ИтогоОплаченоДнейЗаСчетФСС + Выборка.ОплаченоДнейЗаСчетФСС;
			ИтогоОплаченоДнейБольничныйВсего = ИтогоОплаченоДнейБольничныйВсего + Выборка.ОплаченоДнейБольничныйВсего;
			ИтогоДнейСНарушениемРежима = ИтогоДнейСНарушениемРежима + Выборка.ДнейСНарушениемРежима;
		КонецЦикла; 
	КонецЕсли; 
	
	ОблПодвал = Макет.ПолучитьОбласть("ПодвалТаблицы");
	ОблПодвал.Параметры.ИтогБольничныйВсего = ИтогоБольничныйВсего;
	ОблПодвал.Параметры.ИтогЗаСчетФСС = ИтогоЗаСчетФСС;
	ОблПодвал.Параметры.ИтогЗаСчетРаботодателяПо202ФЗ = ИтогоЗаСчетРаботодателяПо202ФЗ;
	ОблПодвал.Параметры.ИтогоОплаченоДнейЗаСчетРаботодателяПо202ФЗ = ИтогоОплаченоДнейЗаСчетРаботодателяПо202ФЗ;
	ОблПодвал.Параметры.ИтогоОплаченоДнейЗаСчетФСС = ИтогоОплаченоДнейЗаСчетФСС;
	ОблПодвал.Параметры.ИтогоОплаченоДнейБольничныйВсего = ИтогоОплаченоДнейБольничныйВсего;
	ОблПодвал.Параметры.ИтогоДнейСНарушениемРежима = ИтогоДнейСНарушениемРежима;
	ТабДок.Вывести(ОблПодвал);
	
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабДок.АвтоМасштаб = Истина;
	
КонецПроцедуры

Процедура ВыбПериодНажатие(Элемент)
	НастройкаПериода = Новый НастройкаПериода;
	НастройкаПериода.РедактироватьКакИнтервал = Истина;
	НастройкаПериода.РедактироватьКакПериод = Истина;
	НастройкаПериода.ВариантНастройки = ВариантНастройкиПериода.Период;
	НастройкаПериода.УстановитьПериод(НачПериода, ?(КонПериода='0001-01-01', КонПериода, КонецДня(КонПериода)));
	Если НастройкаПериода.Редактировать() Тогда
		НачПериода = НастройкаПериода.ПолучитьДатуНачала();
		КонПериода = НастройкаПериода.ПолучитьДатуОкончания();
	КонецЕсли;
КонецПроцедуры
 
////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция ПолучитьПланВидовРасчетаОплатаБольничныхЛистовЗаСчетРаботодателя()
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОсновныеНачисленияОрганизаций.Ссылка
	|ИЗ
	|	ПланВидовРасчета.ОсновныеНачисленияОрганизаций КАК ОсновныеНачисленияОрганизаций
	|ГДЕ
	|	ОсновныеНачисленияОрганизаций.Код = &Код
	|	И ОсновныеНачисленияОрганизаций.ПометкаУдаления = ЛОЖЬ";	
	Запрос.УстановитьПараметр("Код", "00025");
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Если Выборка.Следующий() Тогда
			Возврат Выборка.Ссылка;
		КонецЕсли; 
	Иначе
		Возврат Неопределено;
	КонецЕсли; 	
КонецФункции // ПолучитьПланВидовРасчетаОплатаБольничныхЛистовЗаСчетРаботодателя()
 
